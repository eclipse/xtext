/*******************************************************************************
 * Copyright (c) 2010, 2017 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
module org.eclipse.xtext.generator.grammarAccess.GenerateGrammarAccessTestLanguage

import org.eclipse.emf.mwe2.runtime.workflow.Workflow
import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.TestLanguagesFragments
import org.eclipse.xtext.xtext.generator.*

var projectName = "org.eclipse.xtext.tests"
var runtimeProject = "../${projectName}"
var lineDelimiter = '\n'

Workflow {
	bean = StandaloneSetup {
		platformUri="${runtimeProject}/.."
	}
	
	component = DirectoryCleaner {
		directory="${runtimeProject}/src-gen/org/eclipse/xtext/generator/grammarAccess"
	}
	
	component = XtextGenerator {
		cleaner = {
			enabled = false
		}
		configuration = {
			project = model.project.StandardProjectConfig {
				baseName = projectName
				rootPath = ".."
			}
			code = {
				encoding = 'ISO-8859-1'
				lineDelimiter = lineDelimiter
				preferXtendStubs = false
			}
		}
		
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/generator/grammarAccess/GrammarAccessTestLanguage.xtext"
			referencedResource = "classpath:/org/eclipse/xtext/generator/grammarAccess/ametamodel.genmodel"
			fragment = @TestLanguagesFragments {}
			fragment = grammarAccess.GrammarAccessFragment2 auto-inject {}
		}
	}
}
