/*
 * Since we use the Eclipse Style layout where sources and resources
 * live in the same folders, we need to make some adjustments to Gradle'
 * defaults.
 */

def isTestProject = name.endsWith('tests')
def sourceDirs = ['src', 'xtend-gen', 'src-gen', 'emf-gen']

sourceSets {
    configure(isTestProject? test : main)  {
        java.srcDirs = sourceDirs
        resources {
            srcDirs = sourceDirs
            exclude '**/*.java', '**/*.xtendbin', '**/*.xtend', '**/*._trace'
        }
    }
    configure(isTestProject? main : test)  {
        java.srcDirs = []
        resources.srcDirs = []
    }
}

jar {
    from ('.') {
   	    include 'META-INF/**', 'about*.*', 'plugin.xml', 'schema/**', 'model/**'
   	}
}
