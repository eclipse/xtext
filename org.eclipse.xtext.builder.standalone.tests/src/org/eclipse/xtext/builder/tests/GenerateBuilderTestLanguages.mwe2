/*******************************************************************************
 * Copyright (c) 2010, 2017 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.eclipse.xtext.builder.tests.GenerateBuilderTestLanguages

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*

var rootPath = ".."

Workflow {
	
    component = XtextGenerator {
    	configuration = {
			project = model.project.StandardProjectConfig {
				baseName = "org.eclipse.xtext.builder.standalone.tests"
				rootPath = rootPath
			}
			code = {
				encoding = 'ISO-8859-1'
				lineDelimiter = "\n"
				preferXtendStubs = false
			}
		}
		
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/builder/tests/BuilderTestLanguage.xtext"
			
			fragment = ecore.EMFGeneratorFragment2 {
				emfRuntimeVersion = "2.9"
				jdkLevel = "JDK80"
			}
			fragment = grammarAccess.GrammarAccessFragment2 {}
			fragment = resourceFactory.ResourceFactoryFragment2 {}
			fragment = serializer.SerializerFragment2 {
				generateStub = false
			}
			fragment = parser.antlr.XtextAntlrGeneratorFragment2 {
				options = {
					classSplitting = true
				}
			}
			fragment = validation.ValidatorFragment2 {
				generateStub = false
			}
			fragment = builder.BuilderIntegrationFragment2 {}
		}
	}
}
