allprojects {
	ext.isBuildServer = System.getenv("JOB_NAME") != null
	buildscript {
		repositories {
			if (!isBuildServer) {
				mavenLocal()
			}
			jcenter()
			maven {
				url "https://oss.sonatype.org/content/repositories/snapshots/"
			}
		}
	}
	
	group = 'org.eclipse.xtext'
	version = '2.9.0-SNAPSHOT'
	
	repositories.addAll(rootProject.buildscript.repositories)
	
	plugins.withType(JavaPlugin) {
		project.apply from: "${rootDir}/gradle/eclipse-project-layout.gradle"
		sourceCompatibility = 1.6
		tasks.withType(JavaCompile) {
			options.encoding = 'ISO-8859-1'
		}
		tasks.withType(Test) {
			if (isBuildServer) {
				ignoreFailures = true
			}
		}
	}
}

task compileEclipsePlugins(type: MavenExec) {
	args 'clean', 'compile', '-Pinclude-unpublished'
	workingDir 'maven/org.eclipse.xtext.tycho.parent'
}

class MavenExec extends Exec {
	MavenExec() {
		if (org.gradle.internal.os.OperatingSystem.current().windows) {
			executable 'cmd'
			args '/c', "${project.rootDir}/gradle/maven-wrapper/mvnw.bat"
		} else {
			executable "${project.rootDir}/gradle/maven-wrapper/mvnw"
		}
		if (project.gradle.startParameter.refreshDependencies) {
			args '-U'
		}
		args '-T', '1C'
	}
}