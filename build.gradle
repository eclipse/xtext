/*
 * Root project for xtext-lib.
 */

import java.time.format.DateTimeFormatter
import java.time.LocalDateTime

buildscript {
	apply from: "${rootDir}/gradle/versions.gradle"
	repositories.jcenter()
	dependencies {
		classpath "org.xtext:xtext-gradle-plugin:$versions.xtext_gradle_plugin"
	}
}



apply from: "${rootDir}/gradle/versions.gradle"
apply from: "${rootDir}/gradle/bootstrap-setup.gradle"

ext.buildTime = DateTimeFormatter.ofPattern('yyyyMMdd-HHmm').format(LocalDateTime.now())

subprojects {
	version = rootProject.version
	
	repositories {
		mavenLocal()
		jcenter()
	}
	
	apply plugin: 'java'
	if (findProperty('compileXtend') == 'true') {
		apply plugin: 'org.xtext.xtend'
	}
	apply plugin: 'eclipse'
	apply plugin: 'maven-publish'
	
	apply from: "${rootDir}/gradle/java-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/xtend-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
	apply from: "${rootDir}/gradle/eclipse-project-layout.gradle"
	apply from: "${rootDir}/gradle/manifest-gen.gradle"
	apply from: "${rootDir}/gradle/validation.gradle"
	dependencies {
		compile platform("org.eclipse.xtext:xtext-dev-bom:$project.version")
	}
}

task clean(type: Delete) {
	group 'Build'
	description 'Deletes the local repositories'
	delete 'build'
}
