/*
 * Root project for xtext-xtend. This Gradle build is required for the Xtend Gradle plugin,
 * so it build only the bundles that are necessary for this plugin. The other bundles
 * are built with Tycho.
 */

import java.time.format.DateTimeFormatter
import java.time.LocalDateTime

buildscript {
	apply from: "${rootDir}/gradle/versions.gradle"
	repositories.jcenter()
	dependencies {
		classpath "org.xtext:xtext-gradle-plugin:$versions.xtext_gradle_plugin"
		classpath "io.spring.gradle:dependency-management-plugin:$versions.dependency_management_plugin"
	}
}

plugins {
  id 'org.ajoberstar.grgit' version '2.2.0'
}

apply from: "${rootDir}/gradle/versions.gradle"
apply from: "${rootDir}/gradle/bootstrap-setup.gradle"

ext.buildTime = DateTimeFormatter.ofPattern('yyyyMMdd-HHmm').format(LocalDateTime.now())

subprojects {
	group = 'org.eclipse.xtend'
	version = rootProject.version
	
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	dependencyManagement {
		imports {
			mavenBom "org.eclipse.xtext:xtext-dev-bom:$project.version"
		}
	}
	if (findProperty('compileXtend') == 'true') {
		apply plugin: 'org.xtext.xtend'
	}
	apply plugin: 'eclipse'
	apply plugin: 'maven'
	
	apply from: "${rootDir}/gradle/upstream-repositories.gradle"
	apply from: "${rootDir}/gradle/java-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/xtend-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/eclipse-project-layout.gradle"
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
	apply from: "${rootDir}/gradle/manifest-gen.gradle"
	apply from: "${rootDir}/gradle/validation.gradle"
}

task clean(type: Delete) {
	group 'Build'
	description 'Deletes the local repositories.'
	delete 'build'
}
