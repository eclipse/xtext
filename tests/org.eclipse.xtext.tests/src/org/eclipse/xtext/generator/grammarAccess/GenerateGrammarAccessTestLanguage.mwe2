/*******************************************************************************
 * Copyright (c) 2010 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.eclipse.xtext.generator.grammarAccess.GenerateGrammarAccessTestLanguage

import org.eclipse.emf.mwe2.runtime.workflow.Workflow
import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.TestLanguagesFragments
import org.eclipse.xtext.generator.Generator

var projectName = "org.eclipse.xtext.tests"
var runtimeProject = "../${projectName}"
var lineDelimiter = '\n'

Workflow {
	bean = StandaloneSetup {
		platformUri="${runtimeProject}/.."
	}
	
	component = DirectoryCleaner {
		directory="${runtimeProject}/src-gen/org/eclipse/xtext/generator/grammarAccess"
	}
	
	component = Generator auto-inject {
		pathRtProject = runtimeProject
		projectNameRt = projectName
		
		language = {
			uri = "classpath:/org/eclipse/xtext/generator/grammarAccess/GrammarAccessTestLanguage.xtext"
			fragment = @TestLanguagesFragments {
			}
			fragment = org.eclipse.xtext.generator.grammarAccess.GrammarAccessFragment auto-inject {}
		}
	}
}