/*******************************************************************************
 * Copyright (c) 2008 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 *******************************************************************************/
grammar org.eclipse.xtext.parsetree.formatter.FormatterTestLanguage with org.eclipse.xtext.common.Terminals

generate formattertestlanguage "http://simple/formattertestlanguage"

Root:
	"test" (TestLinewrap | TestIndentation | TestLinewrapMinMax);
	
Line:
	(Decl | Assign | Meth | FqnObj | FqnRef | Enumeration | SuppressedHidden "post" | Space | Datatypes) ";";
	
Decl:	
	type+=ID name+=ID;
	
Assign:
	var=ID op=("="|"+=") "[" (val+=INT ("," val+=INT)*)? "]";
	
Meth:
	"void" name=ID "(" (param+=Param ("," param+=Param)*)? ")";
	
Param:
	name+=ID ":" type+=ID;
	
Space: "space" val=ID;
	
TestLinewrap:
	"linewrap" items+=Line*;
	
TestLinewrapMinMax:
	"wrapminmax" items+=Line*;
	
TestIndentation:
	"indentation" "{" (sub+=TestIndentation | items+=Line)* "}" semi?=";"?;
	
FqnObj:
	"fqn" name=FQN;
	
FQN:
	ID ("." ID)*; 

FqnRef:
	"fqnref" ref=[FqnObj|FQN];
	
Enumeration:
	"enum" val+=Enum1+ (',' val+=Enum1)*;
	
enum Enum1: 
	lit1 | lit2 | lit3;

SuppressedHidden hidden():
	{SuppressedHidden} "`" (vals+=SuppressedHiddenSub ('%' vals+=SuppressedHiddenSub)*)? "`";
	
SuppressedHiddenSub:
	SuppressedHiddenSubSub | SuppressedHiddenSubID;

SuppressedHiddenSubSub hidden(WS):
	'<' idval=ID '>';
	
SuppressedHiddenSubID:
	idval=ID;
	
Datatype1: FQN;
Datatype2: FQN;
Datatype3: FQN;

Datatypes: "datatypes" val1=Datatype1 "kw1" val2=Datatype2 val3=Datatype3 "kw3";