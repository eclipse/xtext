/*******************************************************************************
 * Copyright (c) 2014 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 *******************************************************************************/
grammar org.eclipse.xtext.formatting2.regionaccess.internal.RegionAccessTestLanguage
with org.eclipse.xtext.common.Terminals

generate regionaccesstestlanguage "http://formatter2/regionaccesstestlanguage"

Root:
	Simple | Delegation | Unassigned | PrefixedUnassigned | "5" Expression | "6" Mixed | "7" mixed=Mixed;

Simple:
	"1" name=ID;

Delegation:
	"2" delegate=Delegate;

Unassigned:
	"3" Delegate;

PrefixedUnassigned:
	"4" delegate=PrefixedDelegate;

PrefixedDelegate:
	"prefix" Delegate;

Delegate:
	name=ID;

Expression:
	Primary ({Add.left=current} "+" right=Primary)*;

Primary returns Expression:
	{Named} name=ID | Parenthesized;

Parenthesized returns Expression:
	"(" Expression ")";

Mixed:
	"("
	("unassigned" (ID | Datatype))?
	({Action} |
	Mixed |
	(name=ID | "child" eobj=Mixed | datatype=Datatype | "ref" ref=[Mixed|ID] | lit=Enum))
	")"
	({AssignedAction.child=current} "action" (body=Mixed "end")?)*;

Datatype:
	"datatype" (Datatype | ID);

enum Enum:
	lit1 | lit2;