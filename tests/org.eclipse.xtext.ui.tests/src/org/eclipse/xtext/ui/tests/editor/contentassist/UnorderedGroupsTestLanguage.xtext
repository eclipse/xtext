/*******************************************************************************
 * Copyright (c) 2009 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.ui.tests.editor.contentassist.UnorderedGroupsTestLanguage
 	with org.eclipse.xtext.common.Terminals

generate unorderedGroupsTest "http://www.eclipse.org/xtext/ui/tests/2010/UnorderedGroupsTest"

Model :
     '1' first=SimpleModel
   | '2' second=MandatoryModel
   | '3' thrird=LoopedModel
   | '4' forth=GroupLoopedModel
   | 'bug304681' model=Bug304681Model
;

SimpleModel:
	(
	    visibility=('public'|'private'|'protected')?
	  & static?='static'?
	  & synchronized?='synchronized'?
	  & (abstract?='abstract' | final?='final')?
	)
	'class' name=ID '{' '}'
;

MandatoryModel:
	(
	    visibility=('public'|'private'|'protected')
	  & static?='static'
	  & synchronized?='synchronized'
	  & (abstract?='abstract' | final?='final')?
	)
	'class' name=ID '{' '}'
;
  
LoopedModel:
	(
	    visibility+=('public'|'private'|'protected')*
	  & static+='static'*
	  & synchronized+='synchronized'*
	  & (abstract+='abstract' | final+='final')*
	)
	'class' name=ID '{' '}'
;

GroupLoopedModel:
	(
	    visibility+=('public'|'private'|'protected')
	  & static+='static'
	  & synchronized+='synchronized'
	  & (abstract+='abstract' | final+='final')
	)*
	'class' name=ID '{' '}'
;

Bug304681Model:
	"{" (
    	("short" shortDescription=STRING ";")? &
     	("long"  longDescription=STRING ";")? &
     	("uid" uid=STRING ";")? &
     	(flag?="flag" ";")? &
     	features+=Bug304681Feature*
     ) "}";

Bug304681Feature:
  Bug304681Attribute | Bug304681Reference;
  
Bug304681Attribute:
  'attr' name=ID ';';
  
Bug304681Reference:
  'ref' name=ID ';';
  
