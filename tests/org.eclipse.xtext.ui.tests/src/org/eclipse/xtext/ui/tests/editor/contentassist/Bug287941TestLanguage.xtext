/*******************************************************************************
 * Copyright (c) 2009 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.ui.tests.editor.contentassist.Bug287941TestLanguage
 	hidden(WS, ML_COMMENT, SL_COMMENT)

generate bug287941TestLanguage "http://www.eclipse.org/xtext/ui/common/tests/2009/bug287941TestLanguage"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore


Model:
	(imports+=Import)*
	query=MQLquery
;

Import :
	'import' importURI=STRING;

MQLquery :
	"select" selectEntries+=SelectEntry ("," selectEntries+=SelectEntry)* "from" fromEntries+=FromEntry ("," fromEntries+=FromEntry)* ("where" (whereEntries+=WhereEntry)+)?
;



SelectEntry:
	select=[FromEntry] ("." attribute=[ecore::EAttribute])?
; 

FromEntry:
	type=[ecore::EClass] (withoutsubtypes?="withoutsubtypes")? "as" alias=ID (scopeClause=ScopeClause)?
;

ScopeClause:
	(notIn?="not")? "in" scope=Scope
;

Scope:
	ResourceScope | ElementScope
;

ResourceScope:
	"resources" "{" uris+=STRING ("," uris+=STRING)* "}"
;

ElementScope:
	"elements" "{" uris+=STRING ("," uris+=STRING)* "}"
;

WhereEntry returns WhereEntry:
	AndWhereEntry ({OrWhereEntry.entries+=current} 
    	("or" entries+=AndWhereEntry)+)?
;
 
AndWhereEntry returns WhereEntry:
	ConcreteWhereEntry ({AndWhereEntry.entries+=current} 
    	("and" entries+=ConcreteWhereEntry)+)?

;

ConcreteWhereEntry returns WhereEntry:
	ParWhereEntry | AttributeWhereEntry | NullWhereEntry | ReferenceAliasWhereEntry | AliasWhereEntry | SubselectWhereEntry
;

ParWhereEntry  returns WhereEntry:
	"(" WhereEntry ")"
;

AttributeWhereEntry:
	NumericAttributeWhereEntry | StringAttributeWhereEntry | BooleanAttributeWhereEntry | VariableWhereEntry
;

NumericAttributeWhereEntry:
	DoubleWhereEntry | LongWhereEntry
;

DoubleWhereEntry:
	alias=[FromEntry] "." attribute=[ecore::EAttribute] operator=NumericOperator value=SIGNED_DOUBLE
;

LongWhereEntry:
	alias=[FromEntry] "." attribute=[ecore::EAttribute] operator=NumericOperator value=SINGED_LONG
;

VariableWhereEntry:
	alias=[FromEntry] "." attribute=[ecore::EAttribute] operator=NumericOperator rightAlias=[FromEntry]"." rightAttribute=[ecore::EAttribute]
;

enum NumericOperator:
	lessThen="<" | greaterThen=">" | lessEqual="<=" | greaterEqual=">=" | equal="=" | notEqual="!="
;

StringAttributeWhereEntry:
	alias=[FromEntry] "." attribute=[ecore::EAttribute] operator=StringOperator pattern=STRING
;

enum StringOperator:
	equal="=" | notEqual="!=" | like="like" | notLike="notlike"
;

BooleanAttributeWhereEntry:
	alias=[FromEntry] "." attribute=[ecore::EAttribute] operator=BooleanOperator (isTrue?="true" | "false")
;

enum BooleanOperator:
	equal="=" | notEqual="!="
;

NullWhereEntry:
	alias=[FromEntry] "." feature=[ecore::EStructuralFeature] operator=BooleanOperator "null"
;

ReferenceAliasWhereEntry:
	alias=[FromEntry] "." reference=[ecore::EReference] "=" rightAlias=[FromEntry]  
;

SubselectWhereEntry:
	alias=[FromEntry] "." reference=[ecore::EReference] (notIn?="not")? "in" "("subQuery=MQLquery")"  
;

AliasWhereEntry:
	alias=[FromEntry] "=" rightAlias=[FromEntry]
;


terminal SINGED_LONG returns ecore::ELong: '-'?('0'..'9')+;	
terminal SIGNED_DOUBLE returns ecore::EDouble: '-'?('0'..'9')+ ('.' ('0'..'9')+)?;

terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;