/*******************************************************************************
 * Copyright (c) 2010 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.ui.tests.editor.contentassist.Bug303200TestLanguage hidden(WS)

generate bug303200TestLanguage "http://www.eclipse.org/xtext/ui/common/tests/2010/bug303200TestLanguage"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Program: {Program}
	(LT* directives+=ProgramDirective)*
	LT*;

ProgramDirective
	: FunctionDefinition
	| Statement
	;
FunctionDefinition:
	(attributes+=Attribute)* 'function' LT* name=ID LT*
	params=Parameters LT* body=Block;
	
Attribute
	: ident=ID
	| {BracketAttribute} '[' LT* expression=PostfixExpression LT* ']'
	;
	
Parameters : 
	{Parameters}
	'(' LT* (params+=ID LT* (',' LT* params+=ID LT*)* )? ')';
	
Block : 
	{Block} 
	'{' (LT* directives+=Statement )* LT* '}';

Statement
	: Block
	| {ExpressionStatement} expression=PostfixExpression (';' | LT)
	;
	
PostfixExpression
	: 	PrimaryExpression
		( {PostfixExpression.expression = current} LT* property=PropertyOperator
		| {Invocation.expression = current} LT* '(' LT* (arguments=ListExpression LT*)? ')'
		)*
	;
	
ListExpression:
	expressions+=PostfixExpression (LT* ',' LT* expressions+=PostfixExpression)*;
	
PropertyOperator
	: '.' LT* name=ID
	| '[' LT* expressions=ListExpression LT* ']'
	;
	
PrimaryExpression
	: {Identifier} name=ID
	| {FunctionExpression} 'function' LT* params=Parameters LT* body=Block
	;

terminal ID: ('a'..'z'|'A'..'Z')+;
terminal WS: (' '|'\t')+;
terminal LT: '\r'|'\n';