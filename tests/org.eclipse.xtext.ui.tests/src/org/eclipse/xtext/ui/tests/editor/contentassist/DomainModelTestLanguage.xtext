/*******************************************************************************
 * Copyright (c) 2009 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.ui.tests.editor.contentassist.DomainModelTestLanguage
 	with org.eclipse.xtext.common.Terminals

generate domainModelTest "http://www.eclipse.org/xtext/ui/common/tests/2009/DomainModelTestLanguage"


Model :
	(imports+=Import)*
	(elements+=Type)*;
	
Import :
	'import' importURI=STRING;
	
Type:
	DataType | Class;
	
DataType:
	'datatype' name=ID ';'?;
	
Class :
	'class' name=ID ('extends' superClass=[Class])? '{'
		properties+=Property*
		subClasses += Class*
	'}';

Property:
	Attribute | Reference;
	
Attribute:
	'attr' name=ID ':' type=[DataType] ';'?;
	
Reference:
	'ref' name=ID ':' type=[Class] ';'?;
