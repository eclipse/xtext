/*******************************************************************************
 * Copyright (c) 2008 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 *******************************************************************************/
grammar org.eclipse.xtext.parsetree.reconstr.SimpleReconstrTestLanguage with org.eclipse.xtext.common.Terminals

generate simplerewritetest "http://www.eclipse.org/2009/tmf/xtext/simplerewritetest"

Op returns Expression: 
	Term ({Op.values+=current} values+=Term)*;
	
Term returns Expression:
	Atom | TwoNumbers | ManyStrings | Parens | Type | Ref2 | Spare | Boolean | Transient1 | Consumed1 | Consumed2;
	
Atom: 
	name=ID;

Parens returns Expression: 
	'(' Op ')' em='!'?;
	
TwoNumbers:
	num1=INT num2=INT ('#' num3+=INT)*;

ManyStrings: 
	'=' (str1+=STRING)* str2+=STRING; 

Type :
	'type' name=ID 'extends' ^extends=[Type];

Ref2:
	'#2' ref2=('mykeyword1' | STRING | 'mykeyword2');
	
Spare:
	'#3' id+=ID ('.' id+=ID)*;
	
Boolean:
	'#4' bool?='myoption'? 'kw' value=ID; 

Transient1: 
	'#5' (precStar?='*'|prec=INT) (',' (scaleStar?='*'|scale=INT))?;
	
Consumed1:
	'#6' (('v1' v1+=INT* v2+=ID) | ('v2' v2+=ID* v1+=INT));
	 
Consumed2:
	'#7' child=Consumed1; 
