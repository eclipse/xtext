/*******************************************************************************
 * Copyright (c) 2010 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.eclipse.xtext.builder.tests.GenerateBuilderTestLanguages

import org.eclipse.emf.mwe2.runtime.workflow.Workflow
import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*

var uri = "classpath:/org/eclipse/xtext/builder/tests/BuilderTestLanguage.xtext"
var projectName = "org.eclipse.xtext.builder.tests"
var runtimeProject = "../${projectName}"
var classSplitting = true
var lineDelimiter = '\n'

Workflow {
	
	bean = StandaloneSetup {
		ignoreBrokenProjectFiles = true
		platformUri = "${runtimeProject}/../.."
	}
	
	component = DirectoryCleaner {
		directory = "${runtimeProject}/src-gen"
	}
	
	component = Generator auto-inject {
		pathRtProject = runtimeProject
		pathUiProject = runtimeProject
		projectNameRt = projectName
		activator = "org.eclipse.xtext.builder.tests.Activator" 
		
		language = auto-inject {
			fragment = ecore.EMFGeneratorFragment {
				emfRuntimeVersion = "2.9"
				jdkLevel = "JDK60"
			}
			fragment = grammarAccess.GrammarAccessFragment auto-inject {}
			fragment = resourceFactory.ResourceFactoryFragment {}
			fragment = serializer.SerializerFragment auto-inject {
				srcGenOnly = true
			}
			fragment = parser.antlr.XtextAntlrGeneratorFragment auto-inject {
				options = auto-inject {} 
			}
			fragment = validation.JavaValidatorFragment {}
			fragment = contentAssist.JavaBasedContentAssistFragment {}
			fragment = parser.antlr.XtextAntlrUiGeneratorFragment auto-inject { 
				options = auto-inject {}
			}
			fragment = builder.BuilderIntegrationFragment {}
		}
	}
}
