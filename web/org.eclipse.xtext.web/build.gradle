plugins {
	id 'osgi'
	id 'com.moowork.node' version "0.10"
}

dependencies {
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'org.eclipse.equinox', name: 'common', version: '3.6.200-v20130402-1505'
    compile project(':plugins:org.eclipse.xtext.common.types')
    compile project(':plugins:org.eclipse.xtext.ide')
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile project(':plugins:org.eclipse.xtext.junit4')
    testCompile project(':web:org.eclipse.xtext.web.example.statemachine.ide')
}

jar.manifest {
	name 'Xtext Web Integration'
	vendor 'Eclipse Xtext'
}

node {
	version = '0.11.10'
	download = true
}

task testJs(type: NodeTask) {
	dependsOn(npmInstall)
	inputs.dir new File('src/test/js')
	script = file('node_modules/mocha/bin/mocha')
	args = ['-u', 'tdd', '--reporter', 'dot', 'src/test/js/*.js']
}
check.dependsOn(testJs)

def optResources = file("$buildDir/opt-resources/META-INF/resources/xtext/${project.version}")
sourceSets.main.resources.srcDir "$buildDir/opt-resources"

task copyImages(type: Copy) {
	from 'src/main/css/xtext/images'
	into new File(optResources, 'images')
}
processResources.dependsOn(copyImages)

class OptimizationTask extends com.moowork.gradle.node.task.NodeTask {
	OptimizationTask() {
		dependsOn(project.npmInstall)
		inputs.dir 'src/main/js'
		script = project.file('node_modules/requirejs/bin/r.js')
		def optimizeLogLevel = 2 // WARN
		args = ['-o', 'logLevel=' + optimizeLogLevel]
	}
	def setBuildProfile(File buildProfile) {
		args.add(1, buildProfile.path)
	}
	def setCssInput(File cssInput) {
		inputs.file cssInput
		args += 'cssIn=' + cssInput
	}
	def setOutput(File output) {
		outputs.file output
		args += 'out=' + output.path
	}
	def setOptimize(String optimize) {
		args += 'optimize=' + optimize
		if (optimize != 'none')
			args += 'preserveLicenseComments=false'
	}
}

['Orion', 'Ace', 'CodeMirror'].each { Framework ->
	def framework = Framework.toLowerCase()
	
    task "optimize${Framework}Js" (type: OptimizationTask) {
		buildProfile = file("src/main/js/requirejs-${framework}-config.js")
		output = file(optResources.path + "/xtext-${framework}.js")
		optimize = 'none'
	}
	
	task "optimize${Framework}JsMin" (type: OptimizationTask) {
		buildProfile = file("src/main/js/requirejs-${framework}-config.js")
		output = file(optResources.path + "/xtext-${framework}.min.js")
		optimize = 'uglify'
	}
	
	task "optimize${Framework}Css" (type: OptimizationTask) {
		cssInput = file("src/main/css/xtext/xtext-${framework}.css")
		output = file(optResources.path + "/xtext-${framework}.css")
	}
	
	processResources.dependsOn("optimize${Framework}Js", "optimize${Framework}JsMin", "optimize${Framework}Css")
}
