/*******************************************************************************
 * Copyright (c) 2015 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.eclipse.xtext.web.example.statemachine.GenerateStatemachine

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*
import org.eclipse.xtext.xtext.generator.*

var rootPath = ".."
var baseName = "org.eclipse.xtext.web.example.statemachine"

Workflow {
	
	component = XtextGenerator auto-inject {
		configuration = StateMachineGeneratorModule {
			project = WizardConfig auto-inject {
				genericIde = {
					enabled = true
				}
				web = {
					enabled = true
					root = "../org.eclipse.xtext.web.example.jetty"
					src = "../org.eclipse.xtext.web.example.jetty/src/main/java"
					srcGen = "../org.eclipse.xtext.web.example.jetty/src/main/xtext-gen"
					assets = "../org.eclipse.xtext.web.example.jetty/src/main/webapp"
				}
			}
			code = auto-inject {
				encoding = "UTF-8"
				fileHeader = 
"/*******************************************************************************
 * Copyright (c) 2015 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/"
			}
		}
		language = auto-inject {
			uri = "classpath:/org/eclipse/xtext/web/example/statemachine/Statemachine.xtext"
			fileExtensions = "statemachine"
			
			// Java API to access grammar elements (required by several other fragments)
			fragment = grammarAccess.GrammarAccessFragment2 auto-inject {}

			// generates Java API for the generated EPackages
			fragment = ecore.EMFGeneratorFragment2 auto-inject {}

			fragment = serializer.SerializerFragment2 auto-inject {
				generateStub = false
			}

			// a custom ResourceFactory for use with EMF
			fragment = resourceFactory.ResourceFactoryFragment2 auto-inject {}

			// The antlr parser generator fragment.
			fragment = parser.antlr.XtextAntlrGeneratorFragment2 auto-inject {
			}

			// Xtend-based API for validation
			fragment = validation.ValidatorFragment2 auto-inject {
			    composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}

			// scoping and exporting API
			fragment = scoping.ImportNamespacesScopingFragment2 auto-inject {}
			fragment = exporting.QualifiedNamesFragment2 auto-inject {}

			// generator API
			fragment = generator.GeneratorFragment2 auto-inject {}

			// formatter API
			fragment = formatting.Formatter2Fragment2 auto-inject {}
			
			// a light-weight parser used for content assist
			fragment = adapter.FragmentAdapter {
				fragment = parser.antlr.XtextAntlrUiGeneratorFragment auto-inject {}
			}
			
			// JavaScript-based syntax highlighting
			fragment = web.WebIntegrationFragment auto-inject {
				framework = "Orion"
				highlightingModuleName = "xtext/statemachine-syntax"
			}
			fragment = web.WebIntegrationFragment auto-inject {
				framework = "Ace"
				highlightingModuleName = "xtext/ace-mode-statemachine"
			}
			fragment = web.WebIntegrationFragment auto-inject {
				framework = "CodeMirror"
				highlightingModuleName = "xtext/cm-mode-statemachine"
			}
		}
	}
}
