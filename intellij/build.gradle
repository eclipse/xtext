apply from: 'prepareIntellij.gradle'

allprojects {
	apply plugin: 'base'
	version = '2.8.0-SNAPSHOT'
	
	repositories {
		if (!System.getProperty("JOB_NAME")) {
			mavenLocal()
		}
		mavenCentral()
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	
	configurations {
		intellijShadowJars
	}
	
	uploadIntellijShadowJars {
		repositories {
			flatDir {
				dirs "$rootDir/build/intellijRepository"
			}
		}
	}
}

configure(subprojects.findAll{p->p.name !='intellij-dependencies'}) {
	apply plugin: 'java'
	
	configurations {
		provided
	}
	
	sourceSets.main.compileClasspath += configurations.provided
	
	dependencies {
		provided project(':intellij-dependencies')
		provided "org.eclipse.xtext:org.eclipse.xtext.xtext:${version}"
		provided "org.eclipse.xtext:org.eclipse.xtext.xbase:${version}"
		provided "org.eclipse.xtext:org.eclipse.xtext.ide:${version}"
		provided "org.eclipse.xtend:org.eclipse.xtend.lib:${version}"
	}
	
	sourceSets {
		main {
			java.srcDirs = ['src', 'xtend-gen', 'src-gen']
			resources {
				srcDirs = ['src', 'src-gen']
				exclude '**/*.xtend'
				exclude '**/*.xtext'
				exclude '**/*.mwe2'
			}
		}
	}
	
	jar {
		metaInf {
			from('META-INF')
		}
	}
	
	sourceCompatibility = 1.5
}

//TODO generate this automatically
task createIntellijRepository(type: Copy) {
	dependsOn allprojects.collect{it.uploadIntellijShadowJars}
	from "updatePlugins.xml"
	into "$rootDir/build/intellijRepository"
}