plugins {
	id "com.terrafolio.jenkins" version "1.2.3"
}

ext.jdks = [
	'1.6' : 'Java 6 Oracle',
	'1.7' : 'Oracle JDK 1.7 Autoinstall',
	'1.8' : 'JDK 1.8 Autoinstall'
]

ext.branches = [
	[
		name: 'master',
		displayName: 'master',
		platforms: [
			oldest: [
				target: 'Helios',
				jdk: jdks['1.6']
			],
			latest: [
				target: 'Mars',
				jdk: jdks['1.7']
			],
		]
	],
	[
		name: 'maintenance',
		displayName: 'maintenance',
		platforms: [
			oldest: [
				target: 'Helios',
				jdk: jdks['1.6']
			],
			latest: [
				target: 'Mars',
				jdk: jdks['1.7']
			],
		]
	]
]

jenkins {
	servers {
		xtext {
			url 'https://xtext-builds.itemis.de/jenkins'
			secure true
			if (project.hasProperty('jenkinsUserName')) {
				username jenkinsUserName
			}
			if (project.hasProperty('jenkinsPassword')) {
				password jenkinsPassword
			}
		}
	}
	
	defaultServer servers.xtext
	
	project.file('templates').listFiles().each {templateFile ->
		templates.create(templateFile.name.split('\\.')[0]) {
			xml file(templateFile)
		}
	}
}

apply from: 'pipelines.gradle'
apply from: 'verbatim-jobs.gradle'