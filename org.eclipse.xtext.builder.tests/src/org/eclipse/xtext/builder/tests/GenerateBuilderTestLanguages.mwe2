/*******************************************************************************
 * Copyright (c) 2010, 2017 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
module org.eclipse.xtext.builder.tests.GenerateBuilderTestLanguages

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

var fileHeader = "/*******************************************************************************
 * Copyright (c) 2010, \${year} itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/"

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.xtext.builder.tests"
				rootPath = rootPath
				runtimeTest = {
					enabled = false
				}
				genericIde = {
					enabled = true
					root = "${rootPath}/org.eclipse.xtext.builder.tests"
				}
				eclipsePlugin = {
					name = "org.eclipse.xtext.builder.tests"
					enabled = true
					root = "${rootPath}/org.eclipse.xtext.builder.tests"
				}
				eclipsePluginTest = {
					enabled = false
				}
				createEclipseMetaData = true
			}
			code = {
				preferXtendStubs = false
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = fileHeader
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.xtext.builder.tests.BuilderTestLanguage"
			fileExtensions = "buildertestlanguage"

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			quickFixProvider = {
				generateStub = false
			}
			outline = {
				generateStub = false
			}
			labelProvider = {
				generateStub = false
			}
			generator = {
				generateStub = false
			}
			scopeProvider = {
				generateStub = false
			}
		}
	}
}
