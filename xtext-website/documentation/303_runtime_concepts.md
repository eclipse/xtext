---
layout: documentation
title: Language Implementation
part: Reference Documentation
---

# {{page.title}} {#runtime-concepts}

This chapter describes the platform-independent language features that are not covered by the [grammar language](301_grammarlanguage.html).

All code examples in this chapter are given in the Xtend language, as it is much better suited for these tasks than Java. Please refer to the [Xtend documentation](http://www.xtend-lang.org) for further details. For Java developers it's extremely easy to learn, as the basics are similar and you only need to understand the additional powerful concepts.

## Code Generation {#code-generation}

Once you have a language you probably want to do something with it. There are two options, you can either write an interpreter that inspects the AST and does something based on that or you translate your language to another programming language or configuration files. In this section we're going to show how to implement a code generator for an Xtext-based language.

### IGenerator

If you go with the default MWE workflow for your language and you haven't used Xbase, you are provided with a callback stub that implements [IGenerator]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/generator/IGenerator.java). It has one method that is called from the builder infrastructure whenever a DSL file has changed or should be translated otherwise. The two parameters passed in to this method are:

*   The resource to be processed
*   An instance of [IFileSystemAccess]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/generator/IFileSystemAccess.java)

The [IFileSystemAccess]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/generator/IFileSystemAccess.java) API abstracts over the different file systems the code generator may run over. When the code generator is triggered within the incremental build infrastructure in Eclipse the underlying file system is the one provided by Eclipse, and when the code generator is executed outside Eclipse, say in a headless build, it is `java.io.File`.

A very simple implementation of a code generator for the [state machine example]({{site.src.xtext}}/examples/org.eclipse.xtext.xtext.ui.examples/contents/org.eclipse.xtext.example.fowlerdsl/src/org/eclipse/xtext/example/fowlerdsl/Statemachine.xtext) could be the following:

```xtend
class StatemachineGenerator implements IGenerator {

    override void doGenerate(Resource resource, IFileSystemAccess fsa) {
        fsa.generateFile("relative/path/AllTheStates.txt", '''
            «FOR state : resource.allContents.filter(State).toIterable»
                State «state.name»
            «ENDFOR»
        ''')
    }
}
```

### Output Configurations

You don't want to deal with platform or even installation dependent paths in your code generator, rather you want to be able to configure the code generator with some basic outlet roots where the different generated files should be placed under. This is what output configurations are made for.

By default every language will have a single outlet, which points to `<project-root>/src-gen/`. The files that go here are treated as fully derived and will be erased by the compiler automatically when a new file should be generated. If you need additional outlets or want to have a different default configuration, you need to implement the interface [IOutputConfigurationProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/generator/IOutputConfigurationProvider.java). It's straightforward to understand and the default implementation gives you a good idea about how to implement it.

With this implementation you lay out the basic defaults which can be changed by users on a workspace or per project level using the preferences.

## Validation {#validation}

Static analysis is one of the most interesting aspects when developing a programming language. The users of your languages will be grateful if they get informative feedback as they type. In Xtext there are basically three different kinds of validation.

### Automatic Validation

Some implementation aspects (e.g. the grammar, scoping) of a language have an impact on what is required for a document or semantic model to be valid. Xtext automatically takes care of this.

#### Lexer/Parser: Syntactical Validation {#syntactical-validation}

The syntactical correctness of any textual input is validated automatically by the parser. The error messages are generated by the underlying parser technology. One can use the [ISyntaxErrorMessageProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parser/antlr/ISyntaxErrorMessageProvider.java) API to customize these messages. Any syntax errors can be retrieved from the Resource using the common EMF API: [`Resource.getErrors()`]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/resource/Resource.java) and [`Resource.getWarnings()`]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/resource/Resource.java)

#### Linker: Cross-reference Validation {#linking-validation}

Any broken cross-references can be checked generically. As cross-reference resolution is done lazily (see [linking](#linking)), any broken links are resolved lazily as well. If you want to validate whether all links are valid, you will have to navigate through the model so that all installed EMF proxies get resolved. This is done automatically in the editor.

Similarly to syntax errors, any unresolvable cross-links will be reported and can be obtained through [`Resource.getErrors()`]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/resource/Resource.java) and [`Resource.getWarnings()`]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/resource/Resource.java)

#### Serializer: Concrete Syntax Validation {#concrete-syntax-validation}

The [IConcreteSyntaxValidator]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/IConcreteSyntaxValidator.java) validates all constraints that are implied by a grammar. Meeting these constraints is mandatory for a model to be serialized.

Example:

```xtext
MyRule:
    ({MySubRule} "sub")? (strVal+=ID intVal+=INT)*;
```

This implies several constraints:

1.  Types: only instances of *MyRule* and *MySubRule* are allowed for this rule. Subtypes are prohibited, since the parser never instantiates unknown subtypes.
1.  Features: In case the *MyRule* and *MySubRule* have [EStructuralFeatures]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EStructuralFeature.java) besides *strVal* and *intVal*, only *strVal* and *intVal* may have [non-transient values](#transient-values).
1.  Quantities: The following condition must be true: `strVal.size() == intVal.size()`.
1.  Values: It must be possible to [convert all values](#value-converter) to valid tokens for the used terminal rules *ID* and *INT*.

The typical use case for the concrete syntax validator is validation in non-Xtext-editors that use an [XtextResource]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/XtextResource.java). This is the case when combining GMF and Xtext, for example. Another use case is when the semantic model is modified "manually" (not by the parser) and then serialized again. Since it is very difficult for the serializer to provide meaningful error messages, the concrete syntax validator is executed by default before serialization. A textual Xtext editor itself is *not* a valid use case. Here, the parser ensures that all syntactical constraints are met. Therefore there is no value in additionally running the concrete syntax validator.

There are some limitations to the concrete syntax validator which result from the fact that it treats the grammar as declarative, which is something the parser doesn't always do.

*   Grammar rules containing assigned actions (e.g. `{MyType.myFeature=current}` are ignored. Unassigned actions (e.g. `{MyType}`), however, are supported.
*   Grammar rules that delegate to one or more rules containing assigned actions via unassigned rule calls are ignored.
*   Orders within list-features cannot be validated. e.g. `Rule: (foo+=R1 foo+=R2)*` implies that *foo* is expected to contain instances of *R1* and *R2* in an alternating order.

To use concrete syntax validation you can let Guice inject an instance of [IConcreteSyntaxValidator]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/IConcreteSyntaxValidator.java) and use it directly. Furthermore, there is an [adapter]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/impl/ConcreteSyntaxEValidator.java) which allows to use the concrete syntax validator as an [EValidator]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EValidator.java). You can, for example, enable it in your runtime module, by adding:

```xtend
@SingletonBinding(eager = true)
def Class<? extends ConcreteSyntaxEValidator> bindConcreteSyntaxEValidator() {
    return ConcreteSyntaxEValidator
}
```

To customize error messages please see [IConcreteSyntaxDiagnosticProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/IConcreteSyntaxDiagnosticProvider.java) and subclass [ConcreteSyntaxDiagnosticProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/impl/ConcreteSyntaxDiagnosticProvider.java).

### Custom Validation {#custom-validation}

In addition to the afore mentioned kinds of validation, which are more or less done automatically, you can specify additional constraints specific for your Ecore model. The Xtext language generator will provide you with two Java classes. The first is an abstract class generated to *src-gen/* which extends the library class [AbstractDeclarativeValidator]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/AbstractDeclarativeValidator.java). This one just registers the EPackages for which this validator introduces constraints. The other class is a subclass of that abstract class and is generated to the *src/* folder in order to be edited by you. That is where you put the constraints in.

The purpose of the [AbstractDeclarativeValidator]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/AbstractDeclarativeValidator.java) is to allow you to write constraints in a declarative way - as the class name already suggests. That is instead of writing exhaustive if-else constructs or extending the generated EMF switch you just have to add the [Check]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/Check.java) annotation to any method and it will be invoked automatically when validation takes place. Moreover you can state for what type the respective constraint method is, just by declaring a typed parameter. This also lets you avoid any type casts. In addition to the reflective invocation of validation methods the AbstractDeclarativeValidator provides a few convenient assertions.

The Check annotation has a parameter that can be used to declare when a check should be run, *FAST* will be run whenever a file is modified, *NORMAL* checks will run when saving the file, and *EXPENSIVE* checks are run when explicitly validating the file via the menu option.

All in all this is very similar to how JUnit 4 works. Here is an example written in Xtend:

```xtend
class DomainmodelValidator extends AbstractDomainmodelValidator {
    
    @Check
    def void checkNameStartsWithCapital(Entity entity) {
        if (!Character.isUpperCase(entity.name.charAt(0))) {
            warning("Name should start with a capital", 
                DomainmodelPackage.Literals.TYPE__NAME)
        }
    }
}
```

You can use the [IResourceValidator]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/IResourceValidator.java) to validate a given resource programmatically. Example:

```xtend
@Inject IResourceValidator resourceValidator

def void checkResource(Resource resource) {
    val issues = resourceValidator.validate(resource,
            CheckMode.ALL, CancelIndicator.NullImpl)
    for (issue: issues) {
        switch issue.severity {
            case ERROR:
                println("ERROR: " + issue.message)
            case WARNING:
                println("WARNING: " + issue.message)
        }
    }
}
```

You can also implement quick fixes for individual validation errors and warnings. See the [section on quick fixes](310_eclipse_support.html#quick-fixes) for details.

## Linking {#linking}

The linking feature allows for specification of cross-references within an Xtext grammar. The following things are needed for the linking:

1. Declaration of a [cross-reference in the grammar](301_grammarlanguage.html#cross-references) (or at least in the Ecore model)
1. Specification of linking semantics (usually provided via the [scoping API](#scoping))

### Lazy Linking {#lazy-linking}

Xtext uses lazy linking by default and we encourage users to stick to this because it provides many advantages, one of which is improved performance in all scenarios where you don't have to load all transitively referenced resources. Furthermore it automatically solves situations where one link relies on other links (cyclic linking dependencies are not supported, though).

When parsing a given input string, say

`ref Entity01`

the [LazyLinker]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/linking/lazy/LazyLinker.java) first creates an EMF proxy and assigns it to the corresponding [EReference]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EReference.java). In EMF a proxy is described by a [URI]({{site.src.emf}}/plugins/org.eclipse.emf.common/src/org/eclipse/emf/common/util/URI.java), which points to the real [EObject]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EObject.java). In the case of lazy linking the stored URI comprises of the context information given at parse time, which is the EObject containing the cross-reference, the actual EReference, the list index (in case it's a multi-valued cross-reference) and the string which represented the cross-link in the concrete syntax. The latter usually corresponds to the name of the referenced EObject. In EMF a URI consists of information about the resource the EObject is contained in as well as a so called fragment part, which is used to find the EObject within that resource. When an EMF proxy is resolved, the current [ResourceSet]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/resource/ResourceSet.java) is asked. The resource set uses the first part to obtain (i.e. load if it is not already loaded) the resource. Then the resource is asked to return the EObject based on the fragment in the URI. The actual cross-reference resolution is done by [LazyLinkingResource.getEObject(String)]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/linking/lazy/LazyLinkingResource.java) which receives the fragment and delegates to the implementation of the [ILinkingService]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/linking/ILinkingService.java). The default implementation in turn delegates to the [scoping API](#scoping).

A [simple implementation]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/linking/impl/DefaultLinkingService.java) of the linking service is shipped with Xtext and used for any grammar as default. Usually any necessary customization of the linking behavior can best be described using the [scoping API](#scoping).

## Scoping {#scoping}

Using the scoping API one defines which elements are referable by a given reference. For instance, using the entities example a feature contains a cross-reference to a type:

```domainexample
datatype String

entity HasAuthor {
    author: String
}
```

The grammar rule for features looks like this:

```xtext
Feature:
    (many?='many')? name=ID ':' type=[Type];
```

The grammar declares that for the reference *type* only instances of the type *Type* are allowed. However, this simple declaration doesn't say anything about where to find the type. That is the duty of scopes.

An [IScopeProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/IScopeProvider.java) is responsible for providing an [IScope]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/IScope.java) for a given context [EObject]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EObject.java) and [EReference]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EReference.java). The returned [IScope]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/IScope.java) should contain all target candidates for the given object and cross-reference.

```java
public interface IScopeProvider {

    /**
     * Returns a scope for the given context. The scope
     * provides access to the compatible visible EObjects
     * for a given reference.
     *
     * @param context the element from which an element shall be
     *        referenced
     * @param reference the reference to be used to filter the
     *        elements.
     * @return {@link IScope} representing the inner most
     *         {@link IScope} for the passed context and reference.
     *         Note for implementors: The result may not be
     *         <code>null</code>. Return
     *         <code>IScope.NULLSCOPE</code> instead.
     */
    IScope getScope(EObject context, EReference reference);

}
```

A single [IScope]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/IScope.java) represents an element of a linked list of scopes. That means that a scope can be nested within an outer scope. Each scope works like a symbol table or a map where the keys are strings and the values are so-called [IEObjectDescription]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IEObjectDescription.java), which is effectively an abstract description of a real EObject. In order to create IEObjectDescriptions for your model elements, the class [Scopes]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/Scopes.java) is very useful.

To have a concrete example, let's deal with the following simple grammar.

```xtext
grammar org.xtext.example.mydsl.MyScopingDsl with
                                      org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyScopingDsl"

Root:
    elements+=Element;

Element:
    'element' name=ID ('extends' superElement=[Element])?;

```

If you want to define the scope for the *superElement* cross-reference, the following Xtend code is one way to go.

```xtend
override getScope(EObject context, EReference reference) {
    // We want to define the Scope for the Element's superElement cross-reference
    if (context instanceof Element
            && reference == MyDslPackage.Literals.ELEMENT__SUPER_ELEMENT) {
        // Collect a list of candidates by going through the model
        // EcoreUtil2 provides useful functionality to do that
        // For example searching for all elements within the root Object's tree
        val rootElement = EcoreUtil2.getRootContainer(context)
        val candidates = EcoreUtil2.getAllContentsOfType(rootElement, Element)
        // Create IEObjectDescriptions and puts them into an IScope instance
        return Scopes.scopeFor(candidates)
    }
    return super.getScope(context, reference);
}
```

There are different useful implementations for IScopes shipped with Xtext. We want to mention only some of them here.

The [MapBasedScope]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/impl/MapBasedScope.java) comes with the efficiency of a map to look up a certain name. If you prefer to deal with Multimaps the [MultimapBasedScope]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/impl/MultimapBasedScope.java) should work for you. For situations where some elements should be filtered out of an existing scope, the [FilteringScope]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/impl/FilteringScope.java) is the right way to go. As scopes can be nested, we strongly recommend to use FilteringScope only for leaf scopes without nested scopes.

Coming back to our example, one possible scenario for the FilteringScope could be to exclude the context element from the list of candidates as it should not be a super-element of itself.

```xtend
override getScope(EObject context, EReference reference) {
    if (context instanceof Element
            && reference == MyDslPackage.Literals.ELEMENT__SUPER_ELEMENT) {
        val rootElement = EcoreUtil2.getRootContainer(context)
        val candidates = EcoreUtil2.getAllContentsOfType(rootElement, Element)
        val existingScope = Scopes.scopeFor(candidates)
        // Scope that filters out the context element from the candidates list
        return new FilteringScope(existingScope, [getEObjectOrProxy != context])
    }
    return super.getScope(context, reference);
}
```

### Global Scopes and Resource Descriptions {#global-scopes}

In the simple scoping example above we don't have references across model files. Neither is there a concept like a namespace which would make scoping a bit more complicated. Basically, every *Element* declared in the same resource is visible by its name. However, in the real world things are most likely not that simple: What if you want to reuse certain declared elements across different files and you want to share those as library between different users? You would want to introduce some kind of cross-resource reference.

Defining what is visible from outside the current resource is the responsibility of global scopes. As the name suggests, global scopes are provided by instances of the [IGlobalScopeProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/IGlobalScopeProvider.java). The data structures (called index) used to store its elements are described in the next section.

#### Resource and EObject Descriptions {#resource-descriptions}

In order to make elements of one file referable from another file, you need to export them as part of a so called [IResourceDescription]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java).

An IResourceDescription contains information about the resource itself, which primarily its [URI]({{site.src.emf}}/plugins/org.eclipse.emf.common/src/org/eclipse/emf/common/util/URI.java), a list of exported [EObjects]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EObject.java) in the form of [IEObjectDescriptions]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IEObjectDescription.java), as well as information about outgoing cross-references and qualified names it references. The cross-references contain only resolved references, while the list of imported qualified names also contains the names that couldn't be resolved. This information is leveraged by the indexing infrastructure of Xtext in order to compute the transitive hull of dependent resources.

For users, and especially in the context of scoping, the most important information is the list of exported EObjects. An IEObjectDescription stores the URI of the actual EObject, its [QualifiedName]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/naming/QualifiedName.java), as well as its [EClass]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EClass.java). In addition one can export arbitrary information using the *user data* map. The following diagram gives an overview on the description classes and their relationships.

![The data model of Xtext's index](images/index_datamodel.png)

A language is configured with default implementations of [IResourceDescription.Manager]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java) and [DefaultResourceDescriptionStrategy]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/impl/DefaultResourceDescriptionStrategy.java), which are responsible to compute the list of exported [IEObjectDescriptions]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IEObjectDescription.java). The Manager iterates over the whole EMF model for each [Resource]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/resource/Resource.java) and asks the ResourceDescriptionStrategy to compute an IEObjectDescription for each EObject. The ResourceDescriptionStrategy applies the `getQualifiedName(EObject obj)` from [IQualifiedNameProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/naming/IQualifiedNameProvider.java) on the object, and if it has a qualified name an IEObjectDescription is created and passed back to the Manager which adds it to the list of exported objects. If an EObject doesn't have a qualified name, the element is considered to be not referable from outside the resource and consequently not indexed. If you don't like this behavior, you can implement and bind your own implementation of [IDefaultResourceDescriptionStrategy]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IDefaultResourceDescriptionStrategy.java).

There are two different default implementations of [IQualifiedNameProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/naming/IQualifiedNameProvider.java). Both work by looking up an [EAttribute]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EAttribute.java) '*name*'. The [SimpleNameProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/naming/SimpleNameProvider.java) simply returns the plain value, while the [DefaultDeclarativeQualifiedNameProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/naming/DefaultDeclarativeQualifiedNameProvider.java) concatenates the simple name with the qualified name of its parent exported EObject. This effectively simulates the qualified name computation of most namespace-based languages such as Java. It also allows to override the name computation declaratively: Just add methods named `qualifiedName` in a subclass and give each of them one argument with the type of element you wish to compute a name for.

As already mentioned, the default implementation strategy exports every model element that the IQualifiedNameProvider can provide a name for. This is a good starting point, but when your models become bigger and you have a lot of them the index will become larger and larger. In most scenarios only a small part of your model should be visible from outside, hence only that small part needs to be in the index. In order to do this, bind a custom implementation of [IDefaultResourceDescriptionStrategy]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IDefaultResourceDescriptionStrategy.java) and create index representations only for those elements that you want to reference from outside the resource they are contained in. From within the resource, references to those filtered elements are still possible as long as they have a name.

Beside the exported elements the index contains [IReferenceDescription]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IReferenceDescription.java)s that contain the information who is referencing who. They are created through the IResourceDescription.Manager and IDefaultResourceDescriptionStrategy, too. If there is a model element that references another model element, the IDefaultResourceDescriptionStrategy creates an IReferenceDescription that contains the URI of the referencing element (*sourceEObjectURI*) and the referenced element (*targetEObjectURI*). These IReferenceDescriptions are very useful to find references and calculate affected resources.

As mentioned above, in order to compute an [IResourceDescription]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java) for a resource the framework asks the [IResourceDescription.Manager]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java) which delegates to the IDefaultResourceDescriptionStrategy. To convert between a [QualifiedName]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/naming/QualifiedName.java) and its String representation you can use the [IQualifiedNameConverter]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/naming/IQualifiedNameConverter.java). Here is some Xtend code showing how to do that:

```xtend
@Inject IResourceServiceProvider.Registry rspr
@Inject IQualifiedNameConverter converter

def void printExportedObjects(Resource resource) {
    val resServiceProvider = rspr.getResourceServiceProvider(resource.URI)
    val manager = resServiceProvider.getResourceDescriptionManager()
    val description = manager.getResourceDescription(resource)
    for (eod : description.exportedObjects) {
        println(converter.toString(eod.qualifiedName))
    }
}
```

In order to obtain a [Manager]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java) it is best to ask the corresponding [IResourceServiceProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceServiceProvider.java) as shown above. That is because each language might have a totally different implementation, and as you might refer from one language to a different language you cannot reuse the [Manager]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java) of the first language.

Now that we know how to export elements to be referable from other resources, we need to learn how those exported [IEObjectDescriptions]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IEObjectDescription.java) can be made available to the referencing resources. That is the responsibility of [global scoping]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/IGlobalScopeProvider.java) which is described in the following section.

If you would like to see what's in the index, you could use the 'Open Model Element' dialog from the navigation menu entry.

#### Global Scopes Based On External Configuration {#index-based}

Instead of explicitly referring to imported resources, another option is to have some kind of external configuration in order to define what is visible from outside a resource. Java for instance uses the notion of the class path to define containers (jars and class folders) which contain referenceable elements. In the case of Java the order of such entries is also important.

To enable support for this kind of global scoping in Xtext, a [DefaultGlobalScopeProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/impl/DefaultGlobalScopeProvider.java) has to be bound to the [IGlobalScopeProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/IGlobalScopeProvider.java) interface. By default Xtext leverages the class path mechanism since it is well designed and already understood by most of our users. The available tooling provided by JDT and PDE to configure the class path adds even more value. However, it is just a default: you can reuse the infrastructure without using Java and be independent from the JDT.

In order to know what is available in the "world", a global scope provider which relies on external configuration needs to read that configuration in and be able to find all candidates for a certain [EReference]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EReference.java). If you don't want to force users to have a folder and file name structure reflecting the actual qualified names of the referenceable [EObjects]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EObject.java), you'll have to load all resources up front and either keep holding them in memory or remember all information which is needed for the resolution of cross-references. In Xtext that information is provided by a so-called [IEObjectDescription]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IEObjectDescription.java).

##### About the Index, Containers and Their Manager {#containers}

Xtext ships with an index which remembers all [IResourceDescription]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java) and their [IEObjectDescription]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IEObjectDescription.java) objects. In the IDE-context (i.e. when running the editor, etc.) the index is updated by an incremental project builder. As opposed to that, in a non-UI context you typically do not have to deal with changes, hence the infrastructure can be much simpler. In both situations the global index state is held by an implementation of [IResourceDescriptions]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescriptions.java) (note the plural form!). The bound singleton in the UI scenario is even aware of unsaved editor changes, such that all linking happens to the latest possibly unsaved version of the resources. You will find the Guice configuration of the global index in the UI scenario in [SharedModule]({{site.src.xtext}}/plugins/org.eclipse.xtext.ui.shared/src/org/eclipse/xtext/ui/shared/internal/SharedModule.java).

The index is basically a flat list of instances of [IResourceDescription]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java). The index itself doesn't know about visibility constraints due to class path restriction. Rather than that, they are defined by the referencing language by means of so called [IContainers]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IContainer.java): While Java might load a resource via [ClassLoader.loadResource()]({{site.javadoc.java}}/java/lang/ClassLoader.html) (i.e. using the class path mechanism), another language could load the same resource using the file system paths.

Consequently, the information which container a resource belongs to depends on the referencing context. Therefore an [IResourceServiceProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceServiceProvider.java) provides another interesting service, which is called [IContainer.Manager]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IContainer.java). For a given [IResourceDescription]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java), the Manager provides you with the [IContainer]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IContainer.java) as well as with a list of all IContainers which are visible from there. Note that the [index]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescriptions.java) is globally shared between all languages while the Manager, which adds the semantics of containers, can be very different depending on the language. The following method lists all resources visible from a given [Resource]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/resource/Resource.java):

```xtend
@Inject IContainer.Manager manager
 
def void printVisibleResources(Resource resource, IResourceDescriptions index) {
    val descr = index.getResourceDescription(resource.URI)
    for (visibleContainer : manager.getVisibleContainers(descr, index)) {
        for (visibleResourceDesc : visibleContainer.resourceDescriptions) {
            println(visibleResourceDesc.URI)
        }
    }
}
```

Xtext ships two implementations of [IContainer.Manager]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IContainer.java) which are bound with Guice: The default binding is to [SimpleResourceDescriptionsBasedContainerManager]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/impl/SimpleResourceDescriptionsBasedContainerManager.java), which assumes all [IResourceDescriptions]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IResourceDescription.java) to be in a single common container. If you don't care about container support, you'll be fine with this one. Alternatively, you can bind [StateBasedContainerManager]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/containers/StateBasedContainerManager.java) and an additional [IAllContainersState]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/containers/IAllContainersState.java) which keeps track of the set of available containers and their visibility relationships.

Xtext offers a couple of strategies for managing containers: If you're running an Eclipse workbench, you can define containers based on Java projects and their class paths or based on plain Eclipse projects. Outside Eclipse, you can provide a set of file system paths to be scanned for models. All of these only differ in the bound instance of [IAllContainersState]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/containers/IAllContainersState.java) of the referring language. These will be described in detail in the following sections.

![IContainer Management](images/index_container.png)

##### JDT-Based Container Manager {#jdt-based-containers}

As JDT is an Eclipse feature, this JDT-based container management is only available in the UI scenario. It assumes so called [IPackageFragmentRoots]({{site.javadoc.eclipse-jdt}}/org/eclipse/jdt/core/IPackageFragmentRoot.html) as containers. An IPackageFragmentRoot in JDT is the root of a tree of Java model elements. It usually refers to

*   a source folder of a Java project,
*   a referenced jar,
*   a class path entry of a referenced Java project, or
*   the exported packages of a required PDE plug-in.

So for an element to be referable, its resource must be on the class path of the caller's Java project and it must be exported (as described above).

As this strategy allows to reuse a lot of nice Java things like jars, OSGi, maven, etc. it is part of the default: You should not have to reconfigure anything to make it work. Nevertheless, if you messed something up, make sure you bind

```xtend
def Class<? extends IContainer.Manager> bindIContainer$Manager() {
    return StateBasedContainerManager
}
```

in the runtime module and

```xtend
def Provider<IAllContainersState> provideIAllContainersState() {
    return org.eclipse.xtext.ui.shared.Access.getJavaProjectsState()
}
```

in the Eclipse UI module of the referencing language. The latter looks a bit more difficult than a common binding, as we have to bind a global singleton to a Guice provider. A [StrictJavaProjectsState]({{site.src.xtext}}/plugins/org.eclipse.xtext.ui/src/org/eclipse/xtext/ui/containers/StrictJavaProjectsState.java) requires all elements to be on the class path, while the default [JavaProjectsState]({{site.src.xtext}}/plugins/org.eclipse.xtext.ui/src/org/eclipse/xtext/ui/containers/JavaProjectsState.java) also allows models in non-source folders.

##### Eclipse Project-Based Containers {#project-based-containers}

If the class path based mechanism doesn't work for your case, Xtext offers an alternative container manager based on plain Eclipse projects: Each project acts as a container and the project references (*Properties &rarr; Project References*) are the visible containers.

In this case, your runtime module should use the StateBasedContainerManager as shown above and the Eclipse UI module should bind

```xtend
def Provider<IAllContainersState> provideIAllContainersState() {
    return org.eclipse.xtext.ui.shared.Access.getWorkspaceProjectsState()
}
```

##### ResourceSet-Based Containers {#resource-set-containers}

If you need an [IContainer.Manager]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IContainer.java) that is independent of Eclipse projects, you can use the [ResourceSetBasedAllContainersState]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/containers/ResourceSetBasedAllContainersState.java). This one can be configured with a mapping of container handles to resource URIs.

### Local Scoping {#local-scoping}

We now know how the outer world of referenceable elements can be defined in Xtext. Nevertheless, not everything is available in all contexts and with a global name. Rather than that, each context can usually have a different scope. As already stated, scopes can be nested, i.e. a scope can contain elements of a parent scope in addition to its own elements. When parent and child scope contain different elements with the same name, the parent scope's element will usually be *shadowed* by the element from the child scope.

To illustrate that, let's have a look at Java: Java defines multiple kinds of scopes (object scope, type scope, etc.). For Java one would create the scope hierarchy as commented in the following example:

```java
// file contents scope
import static my.Constants.STATIC;

public class ScopeExample { // class body scope
  private Object field = STATIC;

  private void method(String param) { // method body scope
    String localVar = "bar";
    innerBlock: { // block scope
      String innerScopeVar = "foo";
      Object field = innerScopeVar;
      // the scope hierarchy at this point would look like this:
      //  blockScope{field,innerScopeVar}->
      //  methodScope{localVar, param}->
      //  classScope{field}-> ('field' is shadowed)
      //  fileScope{STATIC}->
      //  classpathScope{
      //      'all qualified names of accessible static fields'} ->
      //  NULLSCOPE{}
      //
    }
    field.add(localVar);
  }
}
```

In fact the class path scope should also reflect the order of class path entries. For instance:

```java
classpathScope{stuff from bin/}
-> classpathScope{stuff from foo.jar/}
-> ...
-> classpathScope{stuff from JRE System Library}
-> NULLSCOPE{}
```

Please find the motivation behind this and some additional details in [this blog post](http://blog.efftinge.de/2009/01/xtext-scopes-and-emf-index.html) .

### Imported Namespace Aware Scoping {#namespace-imports}

The imported namespace aware scoping is based on qualified names and namespaces. It adds namespace support to your language, which is comparable and similar to namespaces in Scala and C#. Scala and C# both allow to have multiple nested packages within one file, and you can put imports per namespace, such that imported names are only visible within that namespace. See the domain model example: its scope provider extends [ImportedNamespaceAwareLocalScopeProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/impl/ImportedNamespaceAwareLocalScopeProvider.java).

#### Importing Namespaces

The [ImportedNamespaceAwareLocalScopeProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/impl/ImportedNamespaceAwareLocalScopeProvider.java) looks up [EAttributes]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EAttribute.java) with name *importedNamespace* and interprets them as import statements.

```xtext
PackageDeclaration:
    'package' name=QualifiedName '{'
        (elements+=AbstractElement)*
    '}';

AbstractElement:
    PackageDeclaration | Type | Import;

QualifiedName:
    ID ('.' ID)*;

Import:
    'import' importedNamespace=QualifiedNameWithWildcard;
  
QualifiedNameWithWildcard:
    QualifiedName '.*'?;
```

Qualified names with or without a wildcard at the end are supported. For an import of a qualified name the simple name is made available as we know from e.g. Java, where `import java.util.Set` makes it possible to refer to [java.util.Set]({{site.javadoc.java}}/java/util/Set.html) by its simple name *Set*. Contrary to Java, the import is not active for the whole file, but only for the namespace it is declared in and its child namespaces. That is why you can write the following in the example DSL:

```domainexample
package foo {
    import bar.Foo
    entity Bar extends Foo {
    }
}

package bar {
    entity Foo {}
}
```

Of course the declared elements within a package are as well referable by their simple name:

```domainexample
package bar {
    entity Bar extends Foo {}
    entity Foo {}
}
```

## Value Converter {#value-converter}

Value converters are registered to convert the parsed text into a data type instance and vice versa. The primary hook is the [IValueConverterService]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/conversion/IValueConverterService.java) and the concrete implementation can be registered via the runtime [Guice module](302_configuration.html#guicemodules). Simply override the corresponding binding in your runtime module like shown in this example:

```xtend
override Class<? extends IValueConverterService> bindIValueConverterService() {
    return MySpecialValueConverterService
}
```

The easiest way to register additional value converters is to make use of [AbstractDeclarativeValueConverterService]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/conversion/impl/AbstractDeclarativeValueConverterService.java), which allows to declaratively register an [IValueConverter]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/conversion/IValueConverter.java) by means of an annotated method.

```xtend
@ValueConverter(rule = "MyRuleName")
def IValueConverter<MyDataType> getMyRuleNameConverter() {
    return new MyValueConverterImplementation()
}
```

If you use the common terminals grammar `org.eclipse.xtext.common.Terminals` you should extend the [DefaultTerminalConverters]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/common/services/DefaultTerminalConverters.java) and override or add value converters by adding the respective methods. In addition to the explicitly defined converters in the default implementation, a delegating converter is registered for each available [EDataType]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EDataType.java). The delegating converter reuses the functionality of the corresponding EMF [EFactory]({{site.src.emf}}/plugins/org.eclipse.emf.ecore/src/org/eclipse/emf/ecore/EFactory.java).

Many languages introduce a concept for qualified names, i.e. names composed of namespaces separated by a delimiter. Since this is such a common use case, Xtext provides an extensible converter implementation for qualified names. The [QualifiedNameValueConverter]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/conversion/impl/QualifiedNameValueConverter.java) handles comments and white space gracefully and is capable to use the appropriate value converter for each segment of a qualified name. This allows for individually quoted segments. The domainmodel example shows how to use it.

The protocol of an [IValueConverter]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/conversion/IValueConverter.java) allows to throw a [ValueConverterException]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/conversion/ValueConverterException.java) if something went wrong. The exception is propagated as a syntax error by the parser or as a validation problem by the [ConcreteSyntaxValidator]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/impl/ConcreteSyntaxValidator.java) if the value cannot be converted to a valid string. The [AbstractLexerBasedConverter]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/conversion/impl/AbstractLexerBasedConverter.java) is useful when implementing a custom value converter. If the converter needs to know about the rule that it currently works with, it may implement the interface [RuleSpecific]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/conversion/IValueConverter.java). The framework will set the rule such that the implementation can use it afterwards.

## Serialization {#serialization}

Serialization is the process of transforming an EMF model into its textual representation. Thereby, serialization complements parsing and lexing.

In Xtext, the process of serialization is split into the following steps:

1.  Validating the semantic model. This is optional, enabled by default, done by the [concrete syntax validator](#concrete-syntax-validation) and can be turned off in the [save options](#save-options).
1.  Matching the model elements with the grammar rules and creating a stream of tokens. This is done by the [parse tree constructor](#parse-tree-constructor).
1.  Associating comments with semantic objects. This is done by the [comment associator](#comment-associater).
1.  Associating existing nodes from the node model with tokens from the token stream.
1.  [Merging existing white space](#hidden-token-merger) and line-wraps into the token stream.
1.  Adding further needed white space or replacing all white space using a [formatter](#formatting).

Serialization is invoked when calling [XtextResource.save(..)]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/XtextResource.java). Furthermore, the [Serializer]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/Serializer.java) provides resource-independent support for serialization. Another situation that triggers serialization is applying [quick fixes](310_eclipse_support.html#quick-fixes) with semantic modifications. Serialization is *not* called when a textual editors contents is saved to disk.

### The Contract {#serialization-contract}

The contract of serialization says that a model which is saved (serialized) to its textual representation and then loaded (parsed) again yields a new model that is equal to the original model. Please be aware that this does *not* imply that loading a textual representation and serializing it back produces identical textual representations. However, the serialization algorithm tries to restore as much information as possible. That is, if the parsed model was not modified in-memory, the serialized output will usually be equal to the previous input. Unfortunately, this cannot be ensured for each and every case. A use case where is is hardly possible, is shown in the following example:

```xtext
MyRule:
  (xval+=ID | yval+=INT)*;
```

The given *MyRule* reads *ID*- and *INT*-elements which may occur in an arbitrary order in the textual representation. However, when serializing the model all *ID*-elements will be written first and then all *INT*-elements. If the order is important it can be preserved by storing all elements in the same list - which may require wrapping the *ID*- and *INT*-elements into other objects.

### Roles of the Semantic Model and the Node Model During Serialization

A serialized document represents the state of the semantic model. However, if there is a node model available (i.e. the semantic model has been created by the parser), the serializer

*   preserves [existing white spaces](#hidden-token-merger) from the node model.
*   preserves [existing comments](#comment-associater) from the node model.
*   preserves the representation of cross-references: If a cross-referenced object can be identified by multiple names (i.e. scoping returns multiple [IEObjectDescriptions]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/IEObjectDescription.java) for the same object), the serializer tries to keep the name that was used in the input file.
*   preserves the representation of values: For values handled by the [value converter](#value-converter), the serializer checks whether the textual representation converted to a value equals the value from the semantic model. If that is true, the textual representation is kept.

### Parse Tree Constructor {#parse-tree-constructor}

The parse tree constructor usually does not need to be customized since it is automatically derived from the [Xtext Grammar](301_grammarlanguage.html). However, it can be helpful to look into it to understand its error messages and its runtime performance.

For serialization to succeed, the parse tree constructor must be able to *consume* every non-transient element of the to-be-serialized EMF model. To *consume* means, in this context, to write the element to the textual representation of the model. This can turn out to be a not-so-easy-to-fulfill requirement, since a grammar usually introduces implicit constraints to the EMF model as explained for the [concrete syntax validator](#concrete-syntax-validation).

If a model can not be serialized, an [XtextSerializationException]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/XtextSerializationException.java) is thrown. Possible reasons are listed below:

*   A model element can not be consumed. This can have the following reasons/solutions:
    *   The model element should not be stored in the model.
    *   The grammar needs an assignment which would consume the model element.
    *   The [transient value service](#transient-values) can be used to indicate that this model element should not be consumed.
*   An assignment in the grammar has no corresponding model element. The default transient value service considers a model element to be transient if it is *unset* or *equals* its default value. However, the parse tree constructor may serialize default values if this is required by a grammar constraint to be able to serialize another model element. The following solution may help to solve such a scenario:
    *   A model element should be added to the model.
    *   The assignment in the grammar should be made optional.
*   The type of the model element differs from the type in the grammar. The type of the model element must be identical to the return type of the grammar rule or the action's type. Subtypes are not allowed.
*   [Value conversion](#value-converter) fails. The value converter can indicate that a value is not serializable by throwing a [ValueConverterException]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/conversion/ValueConverterException.java).
*   An enum literal is not allowed at this position. This can happen if the referenced enum rule only lists a subset of the literals of the actual enumeration.

To understand error messages and performance issues of the parse tree constructor it is important to know that it implements a backtracking algorithm. This basically means that the grammar is used to specify the structure of a tree in which one path (from the root node to a leaf node) is a valid serialization of a specific model. The parse tree constructor's task is to find this path - with the condition that all model elements are consumed while walking this path. The parse tree constructor's strategy is to take the most promising branch first (the one that would consume the most model elements). If the branch leads to a dead end (for example, if a model element needs to be consumed that is not present in the model), the parse tree constructor goes back the path until a different branch can be taken. This behavior has two consequences:

*   In case of an error, the parse tree constructor has found only dead ends but no leaf. It cannot tell which dead end is actually erroneous. Therefore, the error message lists dead ends of the longest paths, a fragment of their serialization and the reason why the path could not be continued at this point. The developer has to judge on his own which reason is the actual error.
*   For reasons of performance, it is critical that the parse tree constructor takes the most promising branch first and detects wrong branches early. One way to achieve this is to avoid having many rules which return the same type and which are called from within the same alternative in the grammar.

### Options {#save-options}

[SaveOptions]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/SaveOptions.java) can be passed to [XtextResource.save(options)]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/XtextResource.java) and to [Serializer.serialize(..)]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/Serializer.java). Available options are:

*   **Formatting.** Default: `false`. If enabled, it is the [formatters](#formatting) job to determine all white space information during serialization. If disabled, the formatter only defines white space information for the places in which no white space information can be preserved from the node model. E.g. When new model elements are inserted or there is no node model.
*   **Validating.** Default: `true`: Run the [concrete syntax validator](#concrete-syntax-validation) before serializing the model.

### Preserving Comments from the Node Model {#comment-associater}

The [ICommentAssociater]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/ICommentAssociater.java) associates comments with semantic objects. This is important in case an element in the semantic model is moved to a different position and the model is serialized, one expects the comments to be moved to the new position in the document as well.

Which comment belongs to which semantic object is surely a very subjective issue. The [default implementation]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/impl/DefaultCommentAssociater.java) behaves as follows, but can be customized:

*   If there is a semantic token before a comment and in the same line, the comment is associated with this token's semantic object.
*   In all other cases, the comment is associated with the semantic object of the next following object.

### Transient Values {#transient-values}

Transient values are values or model elements which are not persisted (written to the textual representation in the serialization phase). If a model contains model elements which can not be serialized with the current grammar, it is critical to mark them transient using the [ITransientValueService]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/ITransientValueService.java), or serialization will fail. The default implementation marks all model elements transient, which are `eStructuralFeature.isTransient()` or not `eObject.eIsSet(eStructuralFeature)`. By default, EMF returns `false` for `eIsSet(..)` if the value equals the default value.

### Unassigned Text {#unassigned-text}

If there are calls of data type rules or terminal rules that do not reside in an assignment, the serializer by default doesn't know which value to use for serialization.

Example:

```xtext
PluralRule:
  'contents:' count=INT Plural;

terminal Plural:
  'item' | 'items';
```

Valid models for this example are `contents 1 item` or `contents 5 items. However, it is not stored in the semantic model whether the keyword *item* or *items* has been parsed. This is due to the fact that the rule call *Plural* is unassigned. However, the [parse tree constructor](#parse-tree-constructor) needs to decide which value to write during serialization. This decision can be be made by customizing the [IValueSerializer.serializeUnassignedValue(EObject, RuleCall, INode)]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/ITokenSerializer.java).

### Cross-Reference Serializer {#cross-reference-serializer}

The cross-reference serializer specifies which values are to be written to the textual representation for cross-references. This behavior can be customized by implementing [ICrossReferenceSerializer]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/ITokenSerializer.java). The default implementation delegates to various other services such as the [IScopeProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/scoping/IScopeProvider.java) or the [LinkingHelper]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/linking/impl/LinkingHelper.java) each of which may be the better place for customization.

### Merge White Space {#hidden-token-merger}

After the [parse tree constructor](#parse-tree-constructor) has done its job to create a stream of tokens which are to be written to the textual representation, and the [comment associator](#comment-associater) has done its work, existing white space form the node model is merged into the stream.

The strategy is as follows: If two tokens follow each other in the stream and the corresponding nodes in the node model follow each other as well, then the white space information in between is kept. In all other cases it is up to the [formatter](#formatting) to calculate new white space information.

### Token Stream {#token-stream}

The [parse tree constructor](#parse-tree-constructor) and the [formatter](#formatting) use an [ITokenStream]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/ITokenStream.java) for their output, and the latter for its input as well. This allows for chaining the two components. Token streams can be converted to a [String]({{site.javadoc.java}}/java/lang/String.html) using the [TokenStringBuffer]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/impl/TokenStringBuffer.java) and to a [Writer]({{site.javadoc.java}}/java/io/Writer.html) using the [WriterTokenStream]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parsetree/reconstr/impl/WriterTokenStream.java).

```java
public interface ITokenStream {

  void flush() throws IOException;
  void writeHidden(EObject grammarElement, String value);
  void writeSemantic(EObject grammarElement, String value);
}
```

## Formatting {#formatting}

Formatting (aka. pretty printing) is the process of rearranging the text in a document to improve the readability without changing the semantic value of the document. Therefore a formatter is responsible for arranging line-wraps, indentation, whitespace, etc. in a text to emphasize its structure, but it is not supposed to alter a document in a way that impacts the semantic model.

The actual formatting is done by constructing a list of text replacements. A text replacement describes a new text which should replace an existing part of the document, described by offset and length. Applying the text replacements turns the unformatted document into a formatted document.

To invoke the formatter programmatically, you need to instantiate a [request]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/formatting2/FormatterRequest.java) and pass it to the [formatter]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/formatting2/IFormatter2.java). The formatter will return a list of text replacements. The document modification itself can be performed by an [ITextRegionRewriter]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/formatting2/regionaccess/ITextRegionRewriter.java).

Implementors of a formatter should extend [AbstractFormatter2]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/formatting2/AbstractFormatter2.java) and add dispatch methods for the model elements that should be formatted. The format routine has to be invoked recursively if the children of an object should be formatted, too.

The following example illustrates that pattern. An instance of PackageDeclaration is passed to the format method along with the current [formattable document]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/formatting2/IFormattableDocument.java). In this scenario, the package name is surrounded by a single space, the curly brace is followed by a new line and increased indentation etc. All elements within that package should be formatted, too, thus `format(..)` is invoked on these as well.

```xtend
def dispatch void format(PackageDeclaration p, extension IFormattableDocument doc) {
    p.regionFor.feature(PACKAGE_DECLARATION__NAME).surround[oneSpace]
    interior(
        p.regionFor.keyword('{').append[newLine],
        p.regionFor.keyword('}'),
        [indent]
    )
    for (element : p.elements) {
        format(element, doc)
        element.append[setNewLines(1, 1, 2)]
    }
}
```

The API is designed in a way that allows to describe the formatting in a declarative way by calling methods on the [IHiddenRegionFormatter]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/formatting2/IHiddenRegionFormatter.java) which is made available inside invocations of `prepend`, `surround` or `append` to specify the formatting rules. This can be done in arbitrary order &ndash; the infrastructure will reorder all the configurations to execute them from top to bottom of the document. If the configuration-based approach is not sufficient for a particular use case, the [document]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/formatting2/IFormattableDocument.java) also accepts imperative logic that is associated with a given range. The [ITextReplacer]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/formatting2/ITextReplacer.java) that can be added directly to the document allows to perform all kinds of modifications to the text in the region that it is associated with.

More detailed information about the API is available as [JavaDoc on the org.eclipse.xtext.formatting2 package]({{site.javadoc.xtext}}/org/eclipse/xtext/formatting2/package-summary.html).

## Character Encoding {#encoding}

Encoding, aka. *character set*, describes the way characters are encoded into bytes and vice versa. Famous standard encodings are *UTF-8* or *ISO-8859-1*. The list of available encodings can be determined by calling [Charset.availableCharsets()]({{site.javadoc.java}}/java/nio/charset/Charset.html). There is also a list of encodings and their canonical Java names in the [API docs](http://download.oracle.com/javase/1.5.0/docs/guide/intl/encoding.doc.html).

Unfortunately, each platform and/or spoken language tends to define its own native encoding, e.g. *Cp1258* on Windows in Vietnamese or *MacIceland* on Mac OS X in Icelandic.

In an Eclipse workspace, files, folders, projects can have individual encodings, which are stored in the hidden file *.settings/org.eclipse.core.resources.prefs* in each project. If a resource does not have an explicit encoding, it inherits the one from its parent recursively. Eclipse chooses the native platform encoding as the default for the workspace root. You can change the default workspace encoding in the Eclipse preferences *Preferences &rarr; Workspace &rarr; Default text encoding*. If you develop on different platforms, you should consider choosing an explicit common encoding for your text or code files, especially if you use special characters.

While Eclipse allows to define and inspect the encoding of a file, your file system usually doesn't. Given an arbitrary text file there is no general strategy to tell how it was encoded. If you deploy an Eclipse project as a jar (even a plug-in), any encoding information not stored in the file itself is lost, too. Some languages define the encoding of a file explicitly, as in the first processing instruction of an XML file. Most languages don't. Others imply a fixed encoding or offer enhanced syntax for character literals, e.g. the unicode escape sequences *\uXXXX* in Java.

As Xtext is about textual modeling, it allows to tweak the encoding in various places.

### Encoding at Language Design Time

The plug-ins created by the *New Xtext Project* wizard are by default encoded in the workspace standard encoding. The same holds for all files that Xtext generates in there. If you want to change that, e.g. because your grammar uses/allows special characters, you should manually set the encoding in the properties of these projects after their creation. Do this before adding special characters to your grammar or at least make sure the grammar reads correctly after the encoding change. To tell the Xtext generator to generate files in the same encoding, set the encoding property in the workflow, e.g.

```mwe2
component = XtextGenerator {
    configuration = {
        code = {
            encoding = "UTF-8"
        }
    ...
```

### Encoding at Language Runtime

As each language could handle the encoding problem differently, Xtext offers a service here. The [IEncodingProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parser/IEncodingProvider.java) has a single method `getEncoding(URI)` to define the encoding of the resource with the given URI. Users can implement their own strategy, but keep in mind that this is not intended to be a long running method. If the encoding is stored within the model file itself, it should be extractable in an easy way, like from the first line in an XML file. The default implementation returns the default Java character set in a standalone scenario.

In the Eclipse UI scenario, when there is a workspace, users will expect the encoding of the model files to be settable the same way as for other files in the workspace. The default implementation of the [IEncodingProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parser/IEncodingProvider.java) in the Eclipse context therefore returns the file's workspace encoding for files in the workspace and delegates to the runtime implementation for all other resources, e.g. models in a jar or from a deployed plug-in. Keep in mind that you are going to lose the workspace encoding information as soon as you leave this workspace, e.g. deploy your project.

Unless you want to enforce a uniform encoding for all models of your language, we advise to override the runtime service only. It is bound in the runtime module using the binding annotation [@Runtime]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/service/DispatchingProvider.java):

```xtend
override configureRuntimeEncodingProvider(Binder binder) {
    binder.bind(IEncodingProvider)
          .annotatedWith(DispatchingProvider.Runtime)
          .to(MyEncodingProvider)
}
```

For a uniform encoding, bind the plain [IEncodingProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parser/IEncodingProvider.java) to the same implementation in all modules. In the Eclipse UI module you can use similar code as above, but with *DispatchingProvider.Ui* instead of *Runtime*.

### Encoding of an XtextResource

An [XtextResource]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/resource/XtextResource.java) uses the [IEncodingProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext/src/org/eclipse/xtext/parser/IEncodingProvider.java) of your language by default. You can override that by passing an option on load and save, e.g.

```xtend
myXtextResource.load(#{XtextResource.OPTION_ENCODING -> "UTF-8"})
```

or

```xtend
myXtextResource.save(#{XtextResource.OPTION_ENCODING -> "ISO-8859-1"})
```

## Unit Testing {#testing}

Automated tests are crucial for the maintainability and the quality of a software product. That is why it is strongly recommended to write unit tests for your language, too. The Xtext project wizard creates test projects for that purpose, which simplify the setup procedure for the basic language implementation as well as platform-specific integrations.

### Creating a Simple Test Class

The core of the test infrastructure is the [XtextRunner]({{site.src.xtext}}/plugins/org.eclipse.xtext.junit4/src/org/eclipse/xtext/junit4/XtextRunner.java) and the language specific [IInjectorProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext.junit4/src/org/eclipse/xtext/junit4/IInjectorProvider.java). Both have to be provided by means of class annotations:

```xtend
import org.eclipse.xtext.junit4.XtextRunner
import org.example.domainmodel.DomainmodelInjectorProvider

@InjectWith(DomainmodelInjectorProvider)
@RunWith(XtextRunner)
class ParserTest {
    
}
```

This configuration will make sure that you can use dependency injection in your test class, and that the global EMF registries are properly populated before and cleaned up after each test.

### Testing the Parser

The class [ParseHelper]({{site.src.xtext}}/plugins/org.eclipse.xtext.junit4/src/org/eclipse/xtext/junit4/util/ParseHelper.java) allows to parse an arbitrary string into an AST model. The AST model itself can be traversed and checked afterwards. A static import of [Assert]({{site.javadoc.junit}}/org/junit/Assert.html) leads to concise and readable test cases.

```xtend
import org.eclipse.xtext.junit4.util.ParseHelper
import static org.junit.Assert.*

...

@Inject ParseHelper<Domainmodel> parser

@Test
def void parseDomainmodel() {
    val model = parser.parse('''
        entity MyEntity {
            parent: MyEntity
        }
    ''')
    val entity = model.elements.head as Entity
    assertSame(entity, entity.features.head.type)
}
```

### Testing Validators {#test-validators}

Testing your validation is very simple with the [ValidationTestHelper]({{site.src.xtext}}/plugins/org.eclipse.xtext.junit4/src/org/eclipse/xtext/junit4/validation/ValidationTestHelper.java):

```xtend
...
 
@Inject extension ParseHelper
@Inject extension ValidationTestHelper

@Test
def void testLowercaseName() {
    val model = "entity foo {}".parse
    model.assertWarning(DomainmodelPackage.Literals.ENTITY, null,
            "Name should start with a capital")
}
```

See the various *assert* methods in that helper class to explore the testing capabilities. You can either assert that a given model has specific issues as in the example above, or assert that it has no issues.

### Testing Multiple Languages

If in addition to the main language your tests require using other languages for references from/to your main language, you'll have to parse and load dependent resources into the same ResourceSet first for cross-reference resolution to work.

As the default generated [IInjectorProvider]({{site.src.xtext}}/plugins/org.eclipse.xtext.junit4/src/org/eclipse/xtext/junit4/IInjectorProvider.java) of your main language (e.g. DomainmodelInjectorProvider) does not know about any other dependent languages, they must be initialized explicitly. The recommended pattern for this is to create a new subclass of the generated *MyLanguageInjectorProvider* in your *\*.test* project and make sure the dependent language is intizialized properly. Then you can use this new injector provider instead of the original one in your test's *@InjectWith*:

```xtend
class MyLanguageWithDependenciesInjectorProvider extends MyLanguageInjectorProvider {
    override internalCreateInjector() {
        MyOtherLangLanguageStandaloneSetup.doSetup
        return super.internalCreateInjector
    }
}

@RunWith(XtextRunner)
@InjectWith(MyLanguageWithDependenciesInjectorProvider)
class YourTest {
    ...
}
```

You should not put injector creation for referenced languages in your standalone setup. Note that for the headless code generation use case, the Maven plug-in is configured with multiple setups, so usually there is no problem there.

You may also need to initialize imported ecore models that are not generated by your Xtext language. This should be done by using an explicit *MyModelPackage.eINSTANCE.getName();* in the *doSetup()* method of your respective language's StandaloneSetup class. Note that it is strongly recommended to follow this pattern instead of just using *@Before* methods in your \*Test class, as due to internal technical reasons that won't work anymore as soon as you have more than just one *@Test*.

```xtend
class MyLanguageStandaloneSetup extends MyLanguageStandaloneSetupGenerated {

    def static void doSetup() {
        if (!EPackage.Registry.INSTANCE.containsKey(MyPackage.eNS_URI))
            EPackage.Registry.INSTANCE.put(MyPackage.eNS_URI, MyPackage.eINSTANCE);
        new MyLanguageStandaloneSetup().createInjectorAndDoEMFRegistration
    }

}
```

This only applies to referencing dependencies to imported Ecore models and languages based on them which may be used in the test. The inherited dependencies from mixed-in grammars are automatically listed in the generated super class already, and nothing needs to be done for those.

---

**[Next Chapter: Integration with Java](305_xbase.html)**
