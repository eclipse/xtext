/*******************************************************************************
 * Copyright (c) 2010, 2018 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
module org.eclipse.xtext.example.domainmodel.GenerateDomainmodelLanguage

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.xtext.generator.ui.codemining.CodeMiningFragment

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.xtext.example.domainmodel"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext\n */"
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.xtext.example.domainmodel.Domainmodel"
			fileExtensions = "dmodel"

			serializer = {
				generateStub = true
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				generateDeprecationValidation = false
			}
			formatter = {
				generateStub = true
				generateXtendStub = true
			}
			generator = {
				generateStub = false
			}
	 		fragment = CodeMiningFragment {
				generateStub = true
				generateXtendStub = false
			}
		}
	}
}
