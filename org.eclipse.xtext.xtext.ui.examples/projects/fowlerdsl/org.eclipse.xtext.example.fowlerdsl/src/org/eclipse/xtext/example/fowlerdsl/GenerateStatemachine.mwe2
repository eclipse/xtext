/*******************************************************************************
 * Copyright (c) 2011, 2019 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
module org.eclipse.xtext.example.fowlerdsl.GenerateStatemachine


import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

var fileHeader = "/*******************************************************************************
 * Copyright (c) 2011, \${year} itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/"

Workflow {

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.xtext.example.fowlerdsl"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				preferXtendStubs = false
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = fileHeader
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.xtext.example.fowlerdsl.Statemachine"
			fileExtensions = "statemachine"

			formatter = {
				generateStub = true
			}
			generator = {
				generateXtendStub = true
			}
			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				generateDeprecationValidation = false
			}
			projectWizard = {
				generate = true
				generateToolbarButton = true
			}
			fileWizard = {
				generate = true
				generateToolbarButton = true
			}
			junitSupport = {
				generateXtendStub = false
				skipXbaseTestingPackage = true
			}
		}
	}
}