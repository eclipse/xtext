/*******************************************************************************
 * Copyright (c) 2010, 2019 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
module org.eclipse.xtext.xtext.ui.GenerateTestLanguages
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var projectName = "org.eclipse.xtext.xtext.ui.tests"

var fileHeader = "/*******************************************************************************
 * Copyright (c) 2010, \${year} itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/"

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "${projectName}"
				rootPath = rootPath
				runtime = {
					name = "${projectName}"
				}
				genericIde = {
					name = "${projectName}"
					enabled = true
				}
				eclipsePlugin = {
					name = "${projectName}"
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "ISO-8859-1"
				lineDelimiter = "\n"
				fileHeader = fileHeader
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.xtext.xtext.ui.ecore2xtext.Ecore2XtextTest"
			fileExtensions = "ecore2xtexttest"
			referencedResource = "platform:/resource/org.eclipse.xtext.xtext.ui.tests/src/org/eclipse/xtext/xtext/ui/ecore2xtext/Ecore2XtextTest.genmodel"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			formatter = {
				generateStub = false
			}
			
			generator = {
				generateStub = false
			}
			
			labelProvider = {
				generateStub = false
			}
			
			quickFixProvider = {
				generateStub = false
			}
			
			outline = {
				generateStub = false
			}

			serializer = {
				generateStub = false
			}
			
			scopeProvider = {
				generateStub = false
			}
			
			parserGenerator = {
				options = {
					classSplitting = true
				}
			}
			
			validator = {
				generateStub = false
			}
			junitSupport = {
				junitVersion = "4"
			}
		}
	}
}
