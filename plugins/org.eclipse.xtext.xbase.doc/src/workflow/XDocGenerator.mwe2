module workflow.XDocGenerator

import org.eclipse.emf.mwe.utils.*

var targetDir = "src-gen"
var fileEncoding = "ISO-8859-1"
var modelPath = "src/"

Workflow {

	component = org.eclipse.xtext.mwe.Reader {
		// lookup all resources on the classpath
		// useJavaClassPath = true

		// or define search scope explicitly
		path = modelPath

		// this class will be generated by the xtext generator 
		register = org.eclipse.xtext.xdoc.XDocStandaloneSetup {}
		load = {
			slot = "doc"
			type = "Document"
		}
	}

	component = org.eclipse.xpand2.Generator {
		expand = "templates::Template::main FOREACH doc"
		outlet = {
			path = targetDir
		}
		fileEncoding = fileEncoding
	}
	
	component = LatexRunner{
		outputDir = targetDir
		inputFile = "${targetDir}/out.tex"
	}
}
