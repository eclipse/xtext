chapter:Xtend_Expressions[Expressions]

In Xtend everything is an expression and has a return type. Statements do not exist. 
That allows you to compose your code in interesting ways. For example, you can have a
code[Xtend][try catch] expression on the right hand side of an assignment: 
code[Xtend][
  val data = try {
      fileContentsToString('data.txt')
    } catch (IOException e) {
      'dummy data'
    }
] 

If code[fileContentsToString()] throws an codeRef[java.io.IOException], it is caught and the string code['dummy data'] is assigned to the value code[data].
 
Expressions can appear as ref:fields[initializers of fields], the body of constructors
or methods and as values in annotations. A method body can either be a ref:blocks[block expression] 
or a ref:templates[template expression].

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:Xtend_Expressions_Literals[Literals]

A literal denotes a fixed, unchangeable value. Literals for ref:stringLiterals[strings], ref:numberLiterals[numbers], ref:booleanLiteral[booleans],
code[Xtend][null] and ref:typeLiteral[Java types] are supported as well as literals for unmodifiable collection types like lists,
sets and maps or literals for arrays.

section2:stringLiterals[String Literals]

A string literal is of type codeRef[java.lang.String].
String literals are enclosed in a pair of single quotes or double quotes. Single quotes are more common
because the signal-to-noise ration is better, but generally you should use the terminals which are
least likely to occur in the string value. Special characters can be quoted with a backslash or defined using
unicode notation. Contrary to Java, strings can span multiple lines.

%% TODO '\u00a1Hola el mundo!' Highlighting for unicode escape sequences

code[Xtend][
  'Hello World !'
  "Hello World !"
  'Hello "World" !'
  "Hello \"World\" !"
  "Hello 
  
    World !"
]

section2:characterLiterals[Character Literals]

Character literals use the same notation as String literals. If a single character literal is used in a context where a primitive code[Xtend][char] or
the wrapper type codeRef[java.lang.Character] is expected, the compiler will treat the literal as such a value or instance.

code[Xtend][
  val char c = 'c'
]

section2:numberLiterals[Number Literals]

Xtend supports roughly the same number literals as Java with a few differences.   
First, there are no signed number literals. If you put a minus operator in front of a number 
literal it is treated as a ref:operators[unary operator] with one argument (the positive number 
literal). Second, as in Java 7, you can separate digits using code[_] for better readability
of large numbers.
 
An integer literal creates an code[Xtend][int], a code[Xtend][long] (suffix code[L]) or a
codeRef[java.math.BigInteger] (suffix code[BI]). There are no octal numbers

code[Xtend][
  42
  1_234_567_890 
  0xbeef    // hexadecimal
  077       // decimal 77 (*NOT* octal)
  -1  // an expression consisting of the unary - operator and an integer literal  
  42L
  0xbeef#L // hexadecimal, mind the '#'
  0xbeef_beef_beef_beef_beef#BI // BigInteger
]

A floating-point literal creates a code[Xtend][double] (suffix code[D] or none), a code[Xtend][float] 
(suffix code[F]) or a codeRef[java.math.BigDecimal] (suffix code[BD]). If you use a code[.] you have to 
specify both, the integral and the fractional part of the mantissa. There are only decimal floating-point 
literals.  
code[Xtend][
  42d     // double
  0.42e2  // implicit double
  0.42e2f // float
  4.2f    // float
  0.123_456_789_123_456_789_123_456_789e2000bd // BigDecimal
]

section2:booleanLiteral[Boolean Literals]

There are two boolean literals, code[Xtend][true] and code[Xtend][false] which correspond to their 
Java counterpart of type code[Xtend][boolean]. 


section2:nullLiteral[Null Literal]

The null pointer literal code[Xtend][null] has exactly the same semantics as in Java.


section2:typeLiteral[Type Literals]

The syntax for type literals is generally the plain name of the type, e.g. the snippet code[Xtend][String]
is equivalent to the Java code code[Java][String.class]. Nested types use the delimiter code['.'].

To disambiguate the expression, type literals may also be specified using the keyword code[Xtend][typeof].

ul[
  item[code[Xtend][Map.Entry] is equivalent to code[Java][Map.Entry.class]]
  item[code[Xtend][typeof(StringBuilder)] yields code[Java][StringBuilder.class]]
]

Consequently it is possible to access the members of a type reflectively by using its plain name code[Xtend][String.getDeclaredFields].

The keyword code[Xtend][typeof] is mandatory for references to array types, e.g. code[Xtend][typeof(int\[\]]

Previous versions of Xtend (2.4.1 and before) used the dollar as the delimiter character for nested types and
enforced the use of code[Xtend][typeof] for all type literals:

ul[
  item[code[Xtend][typeof(Map$Entry)] yields code[Java][Map.Entry.class]]
]


section2:collectionLiterals[Collection Literals]

The methods in codeRef[org.eclipse.xtext.xbase.lib.CollectionLiterals] are automatically imported so
it's very easy and convenient to create instances of the various collection types the JDK offers.

code[Xtend][
  val myList = newArrayList('Hello', 'World')
  val myMap = newLinkedHashMap('a' -> 1, 'b' -> 2) 
]

In addition xtend supports collection literals to create immutable collections and arrays, depending on
the target type. An immutable list can be created like this:

code[Xtend][
  val myList = #\['Hello','World'\]
]

If the target type is an array as in the following example an array is created instead without any
conversion:

code[Xtend][
  val String\[\] myArray = #\['Hello','World'\]
]

An immutable set can be created using curly braces instead of the squared brackets:

code[Xtend][
  val mySet = #{'Hello','World'}
]

An immutable map is created like this:
 
code[Xtend][
  val myMap = #{'a' -> 1 ,'b' ->2}
]

section2:arrays[Arrays]

Java arrays can be created either using a ref:collectionLiterals[literal] as described in the previous
section, or if it should be a new array with a fixed size, one of the 
methods from codeRef[org.eclipse.xtext.xbase.lib.ArrayLiterals] can be used. The generic code[newArrayOfSize(int)]
method works for all reference types, while there is a specific factory method for each primitive type.

Example:
code[Xtend][
  val String\[\] myArray = newArrayOfSize(400)
  val int\[\] intArray = newIntArrayOfSize(400)
]

Retrieving and setting values of arrays is done through the extension methods code[Xtend][get(int)] and 
code[Xtend][set(int, T)] which are specifically overloaded for arrays and are translated directly to the
equivalent native Java code code[Xtend][myArray\[int\]].

Also code[Xtend][length] is available as an extension method and is directly translated to Java's equivalent
code[Java][myArray.length].

Furthermore arrays are automatically converted to lists (code[java.util.List]) when needed. This works similar to how boxing and unboxing between
primitives and their respective wrapper types work.

Example:
code[Xtend][
  val int\[\] myArray = #\[1,2,3\]
  val List<Integer> myList = myArray
]

section:typeCasts[Type Casts]

A type cast behaves exactly like casts in Java, but has a slightly more readable syntax.
Type casts bind stronger than any other operator but weaker than feature calls.

The conformance rules for casts are defined in the 
link[http://docs.oracle.com/javase/specs/jls/se5.0/html/conversions.html#5.5][Java Language Specification].
Here are some examples:
code[Xtend][
  something as MyClass
  42 as Integer
]

Instead of a plain type cast it's also possible to use a ref:switchExpression[switch with a type guard] 
which performs both the casting and the instance-of check. 
ref:polymorphicDispatch[Dispatch methods] are another alternative to casts that offers the potential
to enhance the number of expected and handled types in subclasses.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:operators[Infix Operators and Operator Overloading]

There are a couple of common predefined infix operators. These operators are not limited
to operations on certain types. Instead an operator-to-method mapping allows to redefine the 
operators for any type just by implementing the corresponding method signature. As an example, the 
runtime library contains a class codeRef[org.eclipse.xtext.xbase.lib.BigDecimalExtensions] that defines operators for 
codeRef[java.math.BigDecimal][BigDecimals]. The following code is therefore perfectly valid:
code[Xtend][
  val x = 2.71BD
  val y = 3.14BD
  val sum = x + y    // calls BigDecimalExtension.operator_plus(x,y)
]

This is the complete list of all available operators and their corresponding method signatures:

table[
  tr[td[code[e1 += e2]]       td[code[e1.operator_add(e2)]]]
  tr[td[code[e1 -= e2]]       td[code[e1.operator_remove(e2)]]]
  tr[td[]td[]]
  
  tr[td[code[e1 || e2]]       td[code[e1.operator_or(e2)]]]
  tr[td[]td[]]
  
  tr[td[code[e1 && e2]]       td[code[e1.operator_and(e2)]]]
  tr[td[]td[]]
  
  tr[td[code[e1 == e2]]       td[code[e1.operator_equals(e2)]]]
  tr[td[code[e1 != e2]]       td[code[e1.operator_notEquals(e2)]]]
  tr[td[code[e1 === e2]]      td[code[e1.operator_tripleEquals(e2)]]]
  tr[td[code[e1 !== e2]]      td[code[e1.operator_tripleNotEquals(e2)]]]
  tr[td[]td[]]

  tr[td[code[e1 < e2]]        td[code[e1.operator_lessThan(e2)]]]
  tr[td[code[e1 > e2]]        td[code[e1.operator_greaterThan(e2)]]]
  tr[td[code[e1 <= e2]]       td[code[e1.operator_lessEqualsThan(e2)]]]
  tr[td[code[e1 >= e2]]       td[code[e1.operator_greaterEqualsThan(e2)]]]
  tr[td[]td[]]
  
  tr[td[code[e1 -> e2]]       td[code[e1.operator_mappedTo(e2)]]]
  tr[td[code[e1 .. e2]]       td[code[e1.operator_upTo(e2)]]]
  tr[td[code[e1 >.. e2]]      td[code[e1.operator_greaterThanDoubleDot(e2)]]]
  tr[td[code[e1 ..< e2]]      td[code[e1.operator_doubleDotLessThan(e2)]]]
  tr[td[code[e1 => e2]]       td[code[e1.operator_doubleArrow(e2)]]]
  tr[td[code[e1 << e2]]       td[code[e1.operator_doubleLessThan(e2)]]]
  tr[td[code[e1 >> e2]]       td[code[e1.operator_doubleGreaterThan(e2)]]]
  tr[td[code[e1 <<< e2]]      td[code[e1.operator_tripleLessThan(e2)]]]
  tr[td[code[e1 >>> e2]]      td[code[e1.operator_tripleGreaterThan(e2)]]]
  tr[td[code[e1 <> e2]]       td[code[e1.operator_diamond(e2)]]]
  tr[td[code[e1 ?: e2]]       td[code[e1.operator_elvis(e2)]]]
  tr[td[code[e1 <=> e2]]      td[code[e1.operator_spaceship(e2)]]]
  tr[td[]td[]]
  
  tr[td[code[e1 + e2]]        td[code[e1.operator_plus(e2)]]]
  tr[td[code[e1 - e2]]        td[code[e1.operator_minus(e2)]]]
  tr[td[]td[]]
  
  tr[td[code[e1 * e2]]        td[code[e1.operator_multiply(e2)]]]
  tr[td[code[e1 / e2]]        td[code[e1.operator_divide(e2)]]]
  tr[td[code[e1 % e2]]        td[code[e1.operator_modulo(e2)]]]
  tr[td[code[e1 ** e2]]       td[code[e1.operator_power(e2)]]]
  tr[td[]td[]]
  
  tr[td[code[! e1]]           td[code[e1.operator_not()]]]
  tr[td[code[- e1]]           td[code[e1.operator_minus()]]]
  tr[td[code[+ e1]]           td[code[e1.operator_plus()]]]
]
    
The table above also defines the operator precedence in ascending order. The blank lines separate 
precedence levels. The assignment operators code[+=] and code[-=] are right-to-left associative in the same way 
as the plain assignment operator code[=] is. That is a = b = c is executed as a = (b = c), all
other operators are left-to-right associative. Parentheses can be used to adjust the default 
precedence and associativity. 

section2[Short-Circuit Boolean Operators]

If the operators code[||], code[&&], and code[?:] are bound to the library methods 
codeRef[org.eclipse.xtext.xbase.lib.BooleanExtensions][code[BooleanExtensions.operator_and(boolean l, boolean r)]], 
codeRef[org.eclipse.xtext.xbase.lib.BooleanExtensions][code[BooleanExtensions.operator_or(boolean l, boolean r)]]  resp.
codeRef[org.eclipse.xtext.xbase.lib.ObjectExtensions][code[<T> T operator_elvis(T first, T second)]] the operation is inlined and evaluated in short circuit mode.
That means that the right hand operand might not be evaluated at all in the following cases: 
ol[
  item[in the case of code[||] the operand on the right hand side is not evaluated if the left 
    operand evaluates to code[Xtend][true].]
  item[in the case of code[&&] the operand on the right hand side is not evaluated if the left 
    operand evaluates to code[Xtend][false].]
  item[in the case of code[?:] the operand on the right hand side is not evaluated if the left 
    operand evaluates to code[Xtend][null].]
]

Still you can overload these operators for your types or even override it for booleans, in which case
both operands are always evaluated and the defined method is invoked, i.e. no short-circuit execution is happening.

section2[Postfix Operators]

The two postfix operators code[++] and code[--] use the following method mapping:

table[
  tr[td[code[e1++]]       td[code[e1.operator_plusPlus()]]]
  tr[td[code[e1--]]       td[code[e1.operator_minusMinus()]]]
]

section2[Defined Operators in The Library]

Xtend offers operators for common types from the JDK.

section3[Equality Operators]

In Xtend the equals operators (code[==],code[!=]) are bound to code[Object.equals]. So you can write:

code[Xtend][
  if (name == 'Homer')
  	println('Hi Homer')
]

Java's identity equals semantic is mapped to the tripple-equals operators code[===] and code[!==] in Xtend.

code[Xtend][
  if (someObject === anotherObject)
  	println('same objects')
]

section3[Comparison Operators]

In Xtend the usual comparison operators (code[>],code[<],code[>=], and code[<=]) work as expected on the primitive numbers:

code[Xtend][
  if (42 > myNumber) {
  	...
  }
]

In addition these operators are overloaded for all instances of code[java.lang.Comparable]. So you can
also write

code[Xtend][
  if (startTime < arrivalTime)
  	println("You are too late!")
]

section3[Arithmetic Operators]

The arithmetic operators (code[+],code[-],code[*],code[/],code[%], and code[**]) are not only available for
the primitive types, but also for other reasonable types such as code[BigDecimal] and code[BigInteger].

code[Xtend][
  val x = 2.71BD
  val y = 3.14BD
  val sum = x + y    // calls BigDecimalExtension.operator_plus(x,y)
]

section3[Elvis Operator]

In addition to ref:nullSafeFeatureCalls[null-safe feature calls] Xtend supports the elvis operator known
from Groovy.

code[Xtend][
  val salutation = person.firstName ?: 'Sir/Madam' 
]

The right hand side of the expression is only evaluated if the left side was code[null].

section3[With Operator]

The with operator is very handy when you want to initialize objects or when you want to use a particular
instance a couple of time in subsequent lines of code. It simply passes the left hand side
argument to the lambda on the right hand and returns the left hand after that.

Here's an example:

code[Xtend][
  val person = new Person => \[
  	firstName = 'Homer'
  	lastName = 'Simpson'
  	address = new Address => \[
  		street = '742 Evergreen Terrace'
  		city = 'SpringField'
  	\]
  \]
]

section3[Range Operators]

There are three different range operators. The most useful ones are code[..<] and code[>..] which create exclusive ranges.

code[Xtend][
  // iterate the list forwards
  for (i : 0 ..< list.size) {
  	val element = list.get(i)
  	...
  }
]

code[Xtend][
  // or backwards
  for (i : list.size >.. 0) {
  	val element = list.get(i)
  	...
  }
]

In addition there is the inclusive range, which is nice if you know both ends well. In the movies example
the range is used to check whether a movie was made in a certain decade:

code[Xtend][
  movies.filter\[1980..1989.contains(year)\]
]

Please keep in mind that there are other means to iterator lists, too. For example, you may want to use the code[forEach] extension

code[Xtend][
  list.forEach\[ element, index |
  	.. // if you need access to the current index
  \]
  list.reverseView.forEach\[
    .. // if you just need the element it in reverse order
  \]
]

section3[Pair Operator]

Sometimes you want to use a pair of two elements locally without introducing a new structure. In Xtend you can
use the code[->]-operator which returns an instance of code[Pair<A,B>]:

code[Xtend] [
	val nameAndAge = 'Homer' -> 42
]

If you want to surface a such a pair of values on the interface of a method or field, it's generally a better
idea to use a data class with a well defined name, instead:

code[Xtend] [
	@Data class NameAndAge {
		String name
		int age
	}
]


section2:assignments[Assignments]

ref:variableDeclaration[Local variables] can be assigned using the code[=] operator. 

code[Xtend][
  var greeting = 'Hello'
  if (isInformal)
    greeting = 'Hi'
]

Of course, also non-final fields can be set using an assignment: 
code[Xtend][
  myObj.myField = 'foo'
]

section3:propertyAssignments[Setting Properties]

The lack of properties in Java leads to a lot of syntactic noise when working with data objects.
As Xtend is designed to integrate with existing Java APIs it respects the Java Beans convention,
hence you can call a setter using an assignment:

code[Xtend][
  myObj.myProperty = 'foo' // calls myObj.setMyProperty("foo")
]

The setter is only used if the field is not accessible from the given context. That is why the 
ref:propertyAnnotation[@Property annotation] would rename the local field to code[_myProperty].

The return type of an assignment is the type of the right hand side, in case it is a simple assignment.
If it is translated to a setter method it yields whatever the setter method returns.

section3:assignmentOperators[Assignment Operators]

Compound assignment operators can be used as a shorthand for the assignment of a binary expression.

code[Xtend][
var BigDecimal bd = 45bd
bd += 12bd // equivalent to bd = bd + 12bd
bd -= 12bd // equivalent to bd = bd - 12bd
bd /= 12bd // equivalent to bd = bd / 12bd
bd *= 12bd // equivalent to bd = bd * 12bd
]

Compound assignments work automatically when the infix operator is declared. The following compound assignment operators 
are supported:

table[
  tr[td[code[e1 += e2]]       td[code[+]]]
  tr[td[code[e1 -= e2]]       td[code[-]]]
  tr[td[code[e1 *= e2]]       td[code[*]]]
  tr[td[code[e1 /= e2]]       td[code[/]]]
  tr[td[code[e1 %= e2]]       td[code[%]]]
]

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:blocks[Blocks]

The block expression allows to have imperative code sequences. It consists of a sequence of 
expressions. The value of the last expression in the block is the value of the complete block. 
The type of a block is also the type of the last expression. Empty blocks return code[Xtend][null] and
have the type codeRef[java.lang.Object]. 
ref:variableDeclaration[Variable declarations] are only allowed within blocks and cannot be used as 
a block's last expression.

A block expression is surrounded by curly braces. The expressions in a block can be terminated by an optional semicolon.

Here are two examples:

code[Xtend][
  {
    doSideEffect("foo")
    result
  }
]
code[Xtend][
  {
    var x = greeting;
    if (x.equals("Hello ")) {
      x + "World!" 
    } else {
      x
    }
  }
]


section:variableDeclaration[Variable Declarations]

Variable declarations are only allowed within ref:blocks[blocks]. They are visible from any 
subsequent expressions in the block. 

A variable declaration starting with the keyword code[Xtend][val] denotes a value, which
is essentially a final, unsettable variable. The variable needs to be declared with the 
keyword code[Xtend][var], which stands for 'variable' if it should be allowed to reassign its value.

A typical example for using code[Xtend][var] is a counter in a loop:
code[Xtend][
{
  val max = 100
  var i = 0
  while (i < max) {
    println("Hi there!")
    i = i + 1
  }
}
]

Shadowing variables from outer scopes is not allowed, the only exception is the 
ref:implicitVariables[implicit variable] code[Xtend][it].

Variables declared outside of a lambda expression using the code[Xtend][var] keyword are not accessible from within the
lambda expressions.

A local variable can be marked with the code[extension] keyword to make its methods available as extensions (ref:Extension_Provider[see
extension provider]). 

section2[Typing]

The type of the variable itself can either be explicitly declared or it can be inferred from the initializer
expression. Here is an example for an explicitly declared type:
code[Xtend][
  var List<String> strings = new ArrayList
]

In such cases, the type of the right hand expression must conform to the type 
of the expression on the left side.

Alternatively the type can be inferred from the initializater: 
code[Xtend][
  var strings = new ArrayList<String> // -> msg is of type ArrayList<String>  
]

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:featureCalls[Field Access and Method Invocation]

A simple name can refer to a local field, variable or parameter. In addition
it can point to a method with zero arguments, since empty parentheses are optional. 

section2:propertyAccess[Property Access]

If there is no field with the given name and also no method with the name and zero parameters
accessible, a simple name binds to a corresponding Java-Bean getter method if available:

code[Xtend][
  myObj.myProperty // myObj.getMyProperty()  (.. in case myObj.myProperty is not visible.)
]

section2:implicitVariables[Implicit Variables e[this] and e[it]]

Like in Java the current instance of the class is bound to code[Xtend][this]. This allows for either qualified
field access or method invocations like in:

code[Xtend][
  this.myField
]

or it is possible to omit the receiver:

code[Xtend][
  myField
]

You can use the variable name code[Xtend][it] to get the same behavior for any variable or parameter:

code[Xtend][
  val it = new Person
  name = 'Horst' // translates to 'it.setName("Horst");'
]

Another speciality of the variable code[Xtend][it] is that it is allowed to be shadowed. This is especially useful
when used together with ref:lambdas[lambda expressions].

As code[Xtend][this] is bound to the surrounding object in Java, code[Xtend][it] can be used
in finer-grained constructs such as ref:lambdas[lambda expressions]. That is why code[Xtend][it.myProperty] has 
higher precedence than code[Xtend][this.myProperty].

section2:staticAccess[Static Access]

For accessing a static field or method you can use the recommended Java syntax or the more explicit double colon code[::]. 
That means, the following epxressions are pairwise equivalent:

code[Xtend][
   MyClass.myField
   MyClass::myField
]

code[Xtend][
   com.acme.MyClass.myMethod('foo')
   com.acme.MyClass::myMethod('foo')
   com::acme::MyClass::myMethod('foo')
]

Alternatively you could import the method or field using a ref:imports[static import].

section2:nullSafeFeatureCalls[Null-Safe Feature Call]

Checking for code[Xtend][null] references can make code very unreadable. In many situations it is ok for an 
expression to return code[Xtend][null] if a receiver was code[Xtend][null]. Xtend supports the safe navigation 
operator code[?.] to make such code better readable.

Instead of writing 
code[Java][
  if (myRef != null) myRef.doStuff()
]

one can write
code[Xtend][
  myRef?.doStuff
]

Arguments that would be passed to the method are only evaluated if the method will be invoked at all. 


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:constructorCall[Constructor Call]

Constructor calls have the same syntax as in Java. The only difference is that empty parentheses
are optional:
code[Xtend][
  new String() == new String
  new ArrayList<BigDecimal>() == new ArrayList<BigDecimal>
]

If type arguments are omitted, they will be inferred from the current context similar to Java's
diamond operator on generic method and constructor calls.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:lambdas[Lambda Expressions]

A lambda expression is basically a piece of code, which is wrapped in an object to pass it around. As
a Java developer it is best to think of a lambda expression as an anonymous class with a single method, i.e. 
like in the following Java code :

code[Java][
  // Java Code!
  final JTextField textField = new JTextField();
  textField.addActionListener(new ActionListener() {
    @Override
    public void actionPerformed(ActionEvent e) {
      textField.setText("Something happened!");
    }
  });
]

This kind of anonymous classes can be found everywhere in Java code and have always been the poor-man's replacement
for lambda expressions in Java. 

Xtend not only supports lambda expressions, but offers an extremely dense syntax for it. That is the
code above can be written in Xtend like this:

code[Xtend][
  val textField = new JTextField
  textField.addActionListener(\[ ActionEvent e |
    textField.text = "Something happened!"
  \])
]

As you might have guessed, a lambda expression is surrounded by square brackets (inspired from Smalltalk).
Also a lambda expression like a method declares parameters. The lambda above has one parameter called code[e] which is of
type code[ActionEvent]. You do not have to specify the type explicitly because it can be inferred from
the context:

code[Xtend][
  textField.addActionListener(\[ e |
    textField.text = "Something happened!"
  \])
]

Also as lambdas with one parameter are a common case, there is a special short hand notation for them,
which is to leave the declaration including the vertical bar out. The name of the ref:implicitVariables[single variable]
will be code[Xtend][it] in that case:

code[Xtend][
  textField.addActionListener(\[
    textField.text = "Something happened!"
  \])
]

A lambda expression with zero arguments is written like this (note the bar after the opening bracket):

code[Xtend][
  val Runnable runnable = \[ |
    println("Hello I'm executed!")
  \]
]

When a method call's last parameter is a lambda it can be passed right after the parameter list.
For instance if you want to sort some strings by their length, you could write :

code[Xtend][
  Collections.sort(someStrings) \[ a, b |
    a.length - b.length
  \]
]

which is just the same as writing

code[Xtend][
  Collections.sort(someStrings, \[ a, b |
    a.length - b.length
  \])
]

Since you can leave out empty parentheses for methods which get a lambda as their only argument, you can reduce
the code above further down to:

code[Xtend][
  textField.addActionListener \[
    textField.text = "Something happened!"
  \]
]

A lambda expression also captures the current scope. Any final local variables and all parameters that are visible at construction time can be referred to
from within the lambda body. 
That is exactly what we did with the variable code[textField] above.

The variable code[Xtend][this] refers to the outer class. The lambda instance itself is available with the identifier
code[Xtend][self].

code[Xtend][
	val lineReader = new LineReader(r);
	val AbstractIterator<String> lineIterator = \[|
		val result = lineReader.readLine 
		if (result==null)
			self.endOfData
		return result
	\]
]

section2:closureTypes[Typing]

Lambdas are expressions which produce e[Function] objects. The type of a lambda expression generally depends on the target
type, as seen in the previous examples. That is, the lambda expression can coerce to any interface or abstract class which has declared only 
one abstract method. This allows for using lambda expressions in many existing Java
APIs directly.

However, if you write a lambda expression without having any target type expectation, like in the following
assignment:

code[Xtend][
  val toUpperCaseFunction = \[ String s | s.toUpperCase \] // inferred type is (String)=>String
]

The type will be one of the inner types found in codeRef[org.eclipse.xtext.xbase.lib.Functions] or codeRef[org.eclipse.xtext.xbase.lib.Procedures].
It is a procedure if the return type is code[Xtend][void], otherwise it is a function.

Xtend supports a shorthand syntax for function types. Instead of writing code[Xtend][Function1<? super String,? extends String>]
which is what you will find in the generated Java code, you can simply write code[(String)=>String].

Example:
code[Xtend][
  val (String)=>String stringToStringFunction = \[ toUpperCase \]
  // or
  val Function1<? super String,? extends String> same = \[ toUpperCase \]
  // or
  val stringToStringFunction2 = \[ String s | s.toUpperCase \] // inferred type is (String)=>String
]

Checked exceptions that are thrown in the body of a lambda expression but not declared in the implemented method of the target type are thrown using the 
ref:declaredExceptions[sneaky-throw technique].
Of course you can always ref:Xtend_Expressions_TryCatch[catch and handle] them.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:anonymousClasses[Anonymous Classes]

An anonymous class in Xtend has the very same semantics as in Java (see link[http://docs.oracle.com/javase/specs/jls/se7/html/jls-15.html#jls-15.9.5][Java Language Sepcification]).
Here's an example:

code[Xtend][
val tabListener = new ActionBar.TabListener() {
  override onTabSelected(ActionBar.Tab tab, FragmentTransaction ft) {
    // show the given tab
  }

  override onTabUnselected(ActionBar.Tab tab, FragmentTransaction ft) {
    // hide the given tab
  }

  override onTabReselected(ActionBar.Tab tab, FragmentTransaction ft) {
    // probably ignore this event
  }
};
]

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:ifExpression[If Expression]

An if-expression is used to choose between two different values based on a predicate.

An expression 
code[Xtend][
  if (p) e1 else e2
] 
results in either the value code[e1] or code[e2] depending on whether the predicate code[p] evaluates 
to code[Xtend][true] or code[Xtend][false]. The else part is optional which is a shorthand for an else branch
that returns the default value of the current type, e.g. for reference type this is equivalent to code[Xtend][else null].
That means 

code[Xtend][
  if (foo) x 
]

is a short hand for

code[Xtend][
  if (foo) x else null
]

The type of an if expression is the common super type of the return types code[T1] and code[T2] 
of the two expression code[e1] and code[e2].

While the if expression has the syntax of Java's if statement it behaves more like Java's ternary operator 
(code[Java][predicate ? thenPart : elsePart]), because it is an expression and returns a value.
Consequently, you can use if expressions deeply nested within expressions:

code[Xtend][
  val name = if (firstName != null) firstName + ' ' + lastName else lastName
]

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:switchExpression[Switch Expression]

The switch expression is very different from Java's switch statement. The use of code[Xtend][switch] is not limited 
to certain values but can be used for any object reference. code[Object.equals(Object)] is used to
compare the value in the case with the one you are switching over.
 
Given the following example:
 
code[Xtend][
  switch myString {
    case myString.length > 5 : "a long string."
    case 'some' : "It's some string."
    default : "It's another short string."
  }
]

the main expression code[myString] is evaluated first and then compared to each case sequentially.
If the case expression is of type code[Xtend][boolean], the case matches if the expression evaluates
to code[Xtend][true]. If it is not of type code[Xtend][boolean] it is compared to the value of the main
expression using code[Object.equals(Object)].

If a case is a match, that is it evaluates to code[Xtend][true] or the result equals the one we are switching
over, the case expression after the colon is evaluated and is the result of the whole switch expression.

The main expression can also be a computed value instead of a field or variable. If you want to reuse that value
in the body of the switch expression, you can create a local value for that by using the following notation which
is similar to the syntax in ref:forLoop[code[Xtend][for] loops].

code[Xtend][
  switch myString : someComputation() {
    ..
  }
]

section2[Type guards]

Instead of or in addition to the case guard you can specify a e[type guard]. The case only matches if the 
switch value conforms to this type. A case with both a type guard and a predicate only matches if both conditions match.
If the switch value is a field, parameter or variable, it is automatically casted to the given type within the
predicate and the case body.

code[Xtend][
  def length(Object x) {
    switch x {
      String case x.length > 0 : x.length // length is defined for String 
      List<?> : x.size    // size is defined for List
      default : -1
    }
  }
]

Switches with type guards are a safe and much more readable alternative to instance of / casting cascades you 
might know from Java.

section2[Fall Through]

You can have multiple type guards and cases separated with a comma, to have all of them share the same then part.

code[Xtend][
  def isMale(String salutation) {
    switch salutation {
      case "Mr.", 
      case "Sir" : true
      default : false
    }
  }
] 


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:forLoop[For Loop]

The for loop 
code[Xtend][
  for (T1 variable : arrayOrIterable) expression
] 
is used to execute a certain expression for each element of an array or an instance of 
codeRef[java.lang.Iterable]. The local code[variable] is final, hence cannot be updated. 

The type of a for loop is code[Xtend][void]. The type of the local variable can be inferred from the 
iterable or array that is processed. 

code[Xtend][
  for (String s : myStrings) {
    doSideEffect(s)
  }
  
  for (s : myStrings)
    doSideEffect(s)
]

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:traditionalForLoop[Basic For Loop]

The traditional for loop 
code[Xtend][
  for (<init-expression> ;  <predicate> ; <update-expression>) body-expression
] 
is very similar to the one known from Java, or even C. When executed, it first executes the code[init-expression], 
where local variables can be declared. Next the code[predicate] is executed and if it evaluates to code[true], the code[body-expression] is executed.
On any subsequent iterations the code[update-expressio] is executed instead of the init-expression. This happens until the code[predicate] returns code[false].

The type of a for loop is code[Xtend][void].

code[Xtend][
  for (val i = 0 ; i < s.length ; i++) {
    println(s.subString(0,i)
  }
]

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:whileExpression[While Loop]

A while loop 
code[Xtend][
  while (predicate) expression
] 
is used to execute a certain expression unless the predicate is evaluated to code[Xtend][false]. 
The type of a while loop is code[Xtend][void].

code[Xtend][
  while (true) {
    doSideEffect("foo")
  }
  
  while ((i=i+1) < max) 
    doSideEffect("foo")
]



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:doWhileExpression[Do-While Loop]

A do-while loop 
code[Xtend][
  do expression while (predicate)
] 
is used to execute a certain expression until the predicate is evaluated to code[Xtend][false]. 
The difference to the ref:whileExpression[while loop] is that the execution starts by executing 
the block once before evaluating the predicate for the first time. The type of a do-while 
loop is code[Xtend][void].

code[Xtend][
  do {
    doSideEffect("foo");
  } while (true)
  
  
  do doSideEffect("foo") while ((i=i+1)<max)
]



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:Xtend_Expressions_Return[Return Expression]

A method or lambda expression automatically returns the value of its body expression.
If it is a ref:blocks[block expression] this is the value of the last expression in it.
However, sometimes you want to return early or make it explicit.

The syntax is just like in Java:

code[Xtend][
  listOfStrings.map \[ e | 
    if (e==null) 
      return "NULL"
    e.toUpperCase
  \]
]



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:Xtend_Expressions_Throw[Throwing Exceptions]

Throwing codeRef[java.lang.Throwable]s up the call stack has the same semantics and syntax 
as in Java.

code[Xtend][
{
  ...
  if (myList.isEmpty)
    throw new IllegalArgumentException("the list must not be empty")
  ...
}
]


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:Xtend_Expressions_TryCatch[Try, Catch, Finally]

The try-catch-finally expression is used to handle exceptional situations.
Checked exceptions are treated like runtime exceptions and only optionally validated. 
You can but do not have to catch them as they
will be silently thrown (see the ref:declaredExceptions[section on declared exceptions]).

code[Xtend][
  try {
    throw new RuntimeException()
  } catch (NullPointerException e) {
    // handle e
  } finally {
    // do stuff
  }
]

For try-catch it is again beneficial that it is an expression, because you can write code like the following
and do not have to rely on non-final variables:

code[Xtend][
  val name = try {
      readFromFile
    } catch (IOException e) {
      "unknown"
    }
]

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:Xtend_Expressions_Synchronized[Synchronized]

The synchonized expression does the same as it does in Java (see link[http://docs.oracle.com/javase/specs/jls/se7/html/jls-14.html#jls-14.19][Java Language Specification]).
The only difference is that in Xtend it is an expression and can therefore be used at more places.

code[Xtend][
  synchronized(lock) {
    println("Hello")
  }
] 

code[Xtend][
  val name = synchronized(lock) { 
    doStuff() 
  }
]


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
section:templates[Template Expressions]

Templates allow for readable string concatenation. Templates are surrounded by triple single quotes (code[''']). 
A template expression can span multiple lines and expressions can be nested which are evaluated and their code[toString()] 
representation is automatically inserted at that position.

The terminals for interpolated expression are so called guillemets code[Xtend][«expression»]. They read nicely and are not often used
in text so you seldom need to escape them. These escaping conflicts are the reason why
template languages often use longer character sequences like e.g. code[<%= expression %>] in JSP, for the price of worse readability. 
The downside with the guillemets in Xtend is that you will have to have a consistent encoding. Always use UTF-8 and you are good.

If you use the Eclipse plug-in the guillemets will be inserted on content assist
within a template. They are additionally bound to e[CTRL+SHIFT+<] and e[CTRL+SHIFT+] for code[«] and code[»] respectively.
On a Mac they are also available with e[alt+q] (code[«]) and e[alt+Q] (code[»]).

Let us have a look at an example of how a typical method with a template expressions looks like:

code[Xtend][
def someHTML(String content) '''
  <html>
    <body>
      «content»
    </body>
  </html>
'''
]

As you can see, template expressions can be used as the body of a method.
If an interpolation expression evaluates to code[Xtend][null] an empty string is added.

Template expressions can occur everywhere. 
Here is an example showing it in conjunction with the powerful ref:switchExpression[switch expression]:

code[Xtend][
def toText(Node n) {
  switch n {
    Contents : n.text

    A : '''<a href="«n.href»">«n.applyContents»</a>'''

    default : '''
        <«n.tagName»>
          «n.applyContents»
        </«n.tagName»>
    '''
  }
}
]


section2:templateIF[Conditions in Templates]

There is a special code[Xtend][IF] to be used within templates:

code[Xtend][
def someHTML(Paragraph p) '''
  <html>
    <body>
      «IF p.headLine != null»
        <h1>«p.headline»</h1>
      «ENDIF»
      <p>
        «p.text»
      </p>
    </body>
  </html>
'''
]


section2:templateFOREACH[Loops in Templates]

Also a code[Xtend][FOR] expression is available:

code[Xtend][
def someHTML(List<Paragraph> paragraphs) '''
  <html>
    <body>
      «FOR p : paragraphs»
        «IF p.headLine != null»
          <h1>«p.headline»</h1>
        «ENDIF»
        <p>
          «p.text»
        </p>
      «ENDFOR»
    </body>
  </html>
'''
]

The for expression optionally allows to specify what to prepend (code[Xtend][BEFORE]), put in-between (code[Xtend][SEPARATOR]), and what to
put at the end (code[Xtend][AFTER]) of all iterations. code[Xtend][BEFORE] and code[Xtend][AFTER]
are only executed if there is at least one iteration. (code[Xtend][SEPARATOR]) is only added between
iterations. It is executed if there are at least two iterations.

Here is an example:

code[Xtend][
def someHTML(List<Paragraph> paragraphs) '''
  <html>
    <body>
      «FOR p : paragraphs BEFORE '<div>' SEPARATOR '</div><div>' AFTER '</div>'»
        «IF p.headLine != null»
          <h1>«p.headline»</h1>
        «ENDIF»
        <p>
          «p.text»
        </p>
      «ENDFOR»
    </body>
  </html>
'''
]

section2:templateType[Typing]

The template expression is of type codeRef[java.lang.CharSequence]. It is automatically converted
to codeRef[java.lang.String] if that is the expected target type.


section2:WhitespaceHandling[White Space Handling]

One of the key features of templates is the smart handling of white space in the template output. 
The white space is not written into the output data structure as is but preprocessed. This allows 
for readable templates as well as nicely formatted output. The following three rules are applied 
when the template is evaluated:

ol[
  item[
    Indentation in the template that is relative to a control structure will not be propagated 
    to the output string. A control structure is a code[Xtend][FOR]-loop or a condition 
    (code[Xtend][IF]) as well as the opening and closing marks of the template string itself.
    
    The indentation is considered to be relative to such a control structure if the previous 
    line ends with a control structure followed by optional white space. The amount of indentation white 
    space is not taken into account but the delta to the other lines.
  ]
  item[
    Lines that do not contain any static text which is not white space but do contain control 
    structures or invocations of other templates which evaluate to an empty string, will not 
    appear in the output.
  ]    
  item[
    Any newlines in appended strings (no matter they are created with template expressions or not) will
    be prepended with the current indentation when inserted.
  ]
]

Although this algorithm sounds a bit complicated at first it behaves very intuitively. In addition the
syntax coloring in Eclipse communicates this behavior.

img[images/Xtend_template_coloring.png][Syntax Coloring For Templates In Eclipse][][]

The behavior is best described with a set of examples. The following table assumes a data structure 
of nested nodes.

table[
  tr[
    td[
      code[Xtend][
class Template {
  def print(Node n) '''
    node «n.name» {}
  '''
}
      ]
    ]
    td[
      code[
        node NodeName {}
      ]
    ]
  ]
]

The indentation before code[Xtend][node «n.name»] will be skipped as it is relative to the opening mark of
the template string and thereby not considered to be relevant for the output but only for the readability 
of the template itself.

table[
  tr[
    td[
      code[Xtend][
class Template {
  def print(Node n) '''
    node «n.name» {
      «IF hasChildren»
        «n.children.map\[print\]»
      «ENDIF»
    }
  '''
}
      ]
    ]
    td[
      code[
node Parent{
  node FirstChild {
  }
  node SecondChild {
    node Leaf {
    }
  }
}
      ]
    ]
  ]
]

As in the previous example, there is no indentation on the root level for the same reason.
The first nesting level has only one indentation level in the output. This is derived from
the indentation of the code[Xtend][IF hasChildren] condition in the template which is nested in
the node. The additional nesting of the recursive invocation code[Xtend][children.map\[print\]] is not
visible in the output as it is relative the the surrounding control structure. The line with
code[Xtend][IF] and code[Xtend][ENDIF] contain only control structures thus they are skipped in 
the output. Note the additional indentation of the node e[Leaf] which happens due to the first rule:
Indentation is propagated to called templates.

