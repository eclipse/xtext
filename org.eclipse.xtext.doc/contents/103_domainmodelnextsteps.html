<html>
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" >
	<title>15 Minutes Tutorial - Extended</title>
	<link href="book.css" rel="stylesheet" type="text/css">
	<link href="code.css" rel="stylesheet" type="text/css">
	<link rel="home" href="index.html" title="">
</head>
<body>
<!-- Google Tag Manager -->
<noscript><iframe src="//www.googletagmanager.com/ns.html?id=GTM-TGDS5S"
height="0" width="0" style="display:none;visibility:hidden"></iframe></noscript>
<script>(function(w,d,s,l,i){w[l]=w[l]||[];w[l].push({'gtm.start':
new Date().getTime(),event:'gtm.js'});var f=d.getElementsByTagName(s)[0],
j=d.createElement(s),dl=l!='dataLayer'?'&l='+l:'';j.async=true;j.src=
'//www.googletagmanager.com/gtm.js?id='+i+dl;f.parentNode.insertBefore(j,f);
})(window,document,'script','dataLayer','GTM-TGDS5S');</script>
<!-- End Google Tag Manager -->

<h1 id="domainmodel-next-steps">15 Minutes Tutorial - Extended</h1>

<p>After you have developed you first own DSL, the question arises, how the behavior and the semantics of the language can be customized. Therefore a couple of mini-tutorials are available, that illustrate common use cases when crafting an own DSL.</p>

<p>These lessons are independent from each other. Each of them will be based on the language that was build in the first <a href="102_domainmodelwalkthrough.html">domain model tutorial</a>.</p>

<p>That is, the syntax and the grammar for the language look like this:</p>

<pre><code class="language-domainexample">package java.lang {
  datatype String
}

package my.company.blog {
  import java.lang.*
  import my.company.common.*
    
  entity Blog {
    title: String
    many posts: Post
  }
  
  entity HasAuthor {
    author: String
  }
  
  entity Post extends HasAuthor {
    title: String
    content: String
    many comments: Comment
  }
  
  entity Comment extends HasAuthor {
    content: String
  }
}
</code></pre>

<pre><code class="language-xtext">grammar org.example.domainmodel.Domainmodel with
                                      org.eclipse.xtext.common.Terminals

generate domainmodel "http://www.eclipse.org/xtext/example/Domainmodel"

Domainmodel:
  (elements += AbstractElement)*
;

PackageDeclaration:
  'package' name = QualifiedName '{'
    (elements += AbstractElement)*
  '}'
;

AbstractElement:
  PackageDeclaration | Type | Import
;

QualifiedName:
  ID ('.' ID)*
;

Import:
  'import' importedNamespace = QualifiedNameWithWildcard
;
  
QualifiedNameWithWildcard:
  QualifiedName '.*'?
;
  
Type:
  DataType | Entity
;
  
DataType:
  'datatype' name=ID
;
 
Entity:
  'entity' name = ID 
              ('extends' superType = [Entity | QualifiedName])?
  '{'
    (features += Feature)*
  '}'
;
 
Feature:
  (many ?= 'many')? name = ID ':' type = [Type | QualifiedName]
;
</code></pre>

<h2 id="tutorial-code-generation">Writing a Code Generator With Xtend</h2>

<p>As soon as you generate the Xtext artifacts for a grammar, a code generator stub will be put into the runtime project of your language. Let’s dive into Xtend and see how you can integrate your own code generator with Eclipse.</p>

<p>In this lesson you will generate Java Beans for entities that are defined in the domain model DSL. For each <em>Entity</em>, a Java class is generated and each <em>Feature</em> will lead to a private field in that class and public getters and setters. For the sake of simplicity, we will use fully qualified names all over the generated code.</p>

<pre><code class="language-java">package my.company.blog;

public class HasAuthor {
  private java.lang.String author;
  
  public java.lang.String getAuthor() {
    return author;
  }
  
  public void setAuthor(java.lang.String author) {
    this.author = author;
  }
}
</code></pre>

<p>First of all, locate the file <em>DomainmodelGenerator.xtend</em> in the package <em>org.example.domainmodel.generator</em>. This Xtend class is used to generate code for your models in the standalone scenario and in the interactive Eclipse environment.</p>

<pre><code class="language-xtend">package org.example.domainmodel.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.generator.IFileSystemAccess

class DomainmodelGenerator implements IGenerator {
  override void doGenerate(Resource resource, IFileSystemAccess fsa) {
  }
}
</code></pre>

<p>Let’s make the implementation more meaningful and start writing the code generator. The strategy is to find all entities within a resource and trigger code generation for each one.</p>

<ol>
  <li>
    <p>First of all, you will have to filter the contents of the resource down to the defined entities. Therefore we need to iterate a resource with all its deeply nested elements. This can be achieved with the method <code>getAllContents()</code>. To use the resulting <a href="https://github.com/eclipse/emf/blob/R2_9_0/plugins/org.eclipse.emf.common/src/org/eclipse/emf/common/util/TreeIterator.java">TreeIterator</a> in a for loop, we use the extension method <code>toIterable()</code> from the built-in library class <a href="https://github.com/eclipse/xtext/blob/v2.9.0/plugins/org.eclipse.xtext.xbase.lib/src/org/eclipse/xtext/xbase/lib/IteratorExtensions.java">IteratorExtensions</a>.     </p>

    <pre><code class="language-xtend">class DomainmodelGenerator implements IGenerator {
  override void doGenerate(Resource resource, IFileSystemAccess fsa) {
    for(e: resource.allContents.toIterable.filter(Entity)) {
      ...
    }
  }
}
</code></pre>
  </li>
  <li>
    <p>Now let’s answer the question, how we determine the file name of the Java class, that each <em>Entity</em> should yield. This information should be derived from the qualified name of the <em>Entity</em> since Java enforces this pattern. The qualified name itself has to be obtained from a special service that is available for each language. Fortunately, Xtend allows to reuse that one easily. We simply inject the <a href="https://github.com/eclipse/xtext/blob/v2.9.0/plugins/org.eclipse.xtext/src/org/eclipse/xtext/naming/IQualifiedNameProvider.java">IQualifiedNameProvider</a> into the generator.     </p>

    <pre><code class="language-xtend">  @Inject extension IQualifiedNameProvider
</code></pre>

    <p>This allows to ask for the name of an entity. It is straightforward to convert the name into a file name:     </p>

    <pre><code class="language-xtend">override void doGenerate(Resource resource, IFileSystemAccess fsa) {
  for(e: resource.allContents.toIterable.filter(Entity)) {
    fsa.generateFile(
      e.fullyQualifiedName.toString("/") + ".java",
      e.compile)
  }
}
</code></pre>
  </li>
  <li>
    <p>The next step is to write the actual template code for an entity. For now, the function <code>Entity.compile</code> does not exist, but it is easy to create it:     </p>

    <pre><code class="language-xtend">def compile(Entity e) '''
  package «e.eContainer.fullyQualifiedName»;
      
  public class «e.name» {
  }
'''
</code></pre>
  </li>
  <li>
    <p>This small template is basically the first shot at a Java-Beans generator. However, it is currently rather incomplete and will fail, if the <em>Entity</em> is not contained in a package. A small modification fixes this. The <code>package</code>-declaration has to be wrapped in an <code>IF</code> expression:     </p>

    <pre><code class="language-xtend">def compile(Entity e) '''
  «IF e.eContainer.fullyQualifiedName != null»
    package «e.eContainer.fullyQualifiedName»;
  «ENDIF»
      
  public class «e.name» {
  }
'''
</code></pre>

    <p>Let’s handle the <em>superType</em> of an <em>Entity</em> gracefully, too by using another <code>IF</code> expression:     </p>

    <pre><code class="language-xtend">def compile(Entity e) ''' 
  «IF e.eContainer.fullyQualifiedName != null»
    package «e.eContainer.fullyQualifiedName»;
  «ENDIF»
      
  public class «e.name» «IF e.superType != null
          »extends «e.superType.fullyQualifiedName» «ENDIF»{
  }
'''
</code></pre>
  </li>
  <li>
    <p>Even though the template will compile the <em>Entities</em> without any complains, it still lacks support for the Java properties, that each of the declared features should yield. For that purpose, you have to create another Xtend function that compiles a single feature to the respective Java code.     </p>

    <pre><code class="language-xtend">def compile(Feature f) '''
  private «f.type.fullyQualifiedName» «f.name»;
      
  public «f.type.fullyQualifiedName» get«f.name.toFirstUpper»() {
    return «f.name»;
  }
      
  public void set«f.name.toFirstUpper»(«f.type.fullyQualifiedName» «f.name») {
    this.«f.name» = «f.name»;
  }
'''
</code></pre>

    <p>As you can see, there is nothing fancy about this one. Last but not least, we have to make sure that the function is actually used.     </p>

    <pre><code class="language-xtend">def compile(Entity e) ''' 
  «IF e.eContainer.fullyQualifiedName != null»
    package «e.eContainer.fullyQualifiedName»;
  «ENDIF»
      
  public class «e.name» «IF e.superType != null
          »extends «e.superType.fullyQualifiedName» «ENDIF»{
    «FOR f:e.features»
      «f.compile»
    «ENDFOR»
  }
'''
</code></pre>
  </li>
</ol>

<p>The final code generator looks pretty much like the following code snippet. Now you can give it a try! Launch a new Eclipse Application (<em>Run As → Eclipse Application</em> on the Xtext project) and create a <em>dmodel</em> file in a Java Project. Now simply create a new source folder <em>src-gen</em> in the that project and see how the compiler will pick up your sample <em>Entities</em> and generate Java code for them. </p>

<pre><code class="language-xtend">package org.example.domainmodel.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.generator.IFileSystemAccess
import org.example.domainmodel.domainmodel.Entity
import org.example.domainmodel.domainmodel.Feature
import org.eclipse.xtext.naming.IQualifiedNameProvider

import com.google.inject.Inject

class DomainmodelGenerator implements IGenerator {

  @Inject extension IQualifiedNameProvider

  override void doGenerate(Resource resource, IFileSystemAccess fsa) {
    for(e: resource.allContents.toIterable.filter(Entity)) {
      fsa.generateFile(
        e.fullyQualifiedName.toString("/") + ".java",
        e.compile)
    }
  }

  def compile(Entity e) ''' 
    «IF e.eContainer.fullyQualifiedName != null»
      package «e.eContainer.fullyQualifiedName»;
    «ENDIF»
    
    public class «e.name» «IF e.superType != null
            »extends «e.superType.fullyQualifiedName» «ENDIF»{
      «FOR f:e.features»
        «f.compile»
      «ENDFOR»
    }
  '''

  def compile(Feature f) '''
    private «f.type.fullyQualifiedName» «f.name»;
    
    public «f.type.fullyQualifiedName» get«f.name.toFirstUpper»() {
      return «f.name»;
    }
    
    public void set«f.name.toFirstUpper»(«f.type.fullyQualifiedName» «f.name») {
      this.«f.name» = «f.name»;
    }
  '''
}
</code></pre>

<p>If you want to play around with Xtend, you can try to use the Xtend tutorial which can be materialized into your workspace. Simply choose <em>New → Example → Xtend Examples → Xtend Introductory Examples</em> and have a look at Xtend’s features. As a small exercise, you could implement support for the <em>many</em> attribute of a <em>Feature</em> or enforce naming conventions, e.g. field names should start with an underscore.</p>

<h2 id="tutorial-unit-tests">Unit Testing the Language</h2>

<p>Automated tests are crucial for the maintainability and the quality of a software product. That is why it is strongly recommended to write unit tests for your language, too. The Xtext project wizard creates a test project for that purpose. It simplifies the setup procedure both for the Eclipse agnostic tests and the UI tests for Junit4.</p>

<p>This tutorial is about testing the parser and the linker for the <em>Domainmodel</em>. It leverages Xtend to write the test case.</p>

<ol>
  <li>
    <p>First of all, a new Xtend class has to be created. Therefore, choose the src folder of the test plugin, and select <em>New → Xtend Class</em> from the context menu. Provide a meaningful name and enter the package before you hit finish. The core of the test infrastructure is the <a href="https://github.com/eclipse/xtext/blob/v2.9.0/plugins/org.eclipse.xtext.junit4/src/org/eclipse/xtext/junit4/XtextRunner.java">XtextRunner</a> and the language specific <a href="https://github.com/eclipse/xtext/blob/v2.9.0/plugins/org.eclipse.xtext.junit4/src/org/eclipse/xtext/junit4/IInjectorProvider.java">IInjectorProvider</a>. Both have to be provided by means of class annotations:     </p>

    <pre><code class="language-xtend">  import org.eclipse.xtext.junit4.XtextRunner
  import org.example.domainmodel.DomainmodelInjectorProvider
      
  @InjectWith(DomainmodelInjectorProvider)
  @RunWith(XtextRunner)
  class ParserTest {
  }
</code></pre>
  </li>
  <li>
    <p>The actual test case is pretty straight forward with Xtend. The utility class <em>org.eclipse.xtext.junit4.util.ParseHelper</em> allows to parse an arbitrary string into a <em>Domainmodel</em>. The model itself can be traversed and checked afterwards. A static import of <a href="http://junit.sourceforge.net/javadoc/org/junit/Assert.html">Assert</a> leads to concise and readable test cases.     </p>

    <pre><code class="language-xtend">  import org.eclipse.xtext.junit4.util.ParseHelper
  import static org.junit.Assert.*
      
  ...
    
  @Inject
  ParseHelper&lt;Domainmodel&gt; parser
      
  @Test 
  def void parseDomainmodel() {
    val model = parser.parse(
      "entity MyEntity {
        parent: MyEntity
      }")
    val entity = model.elements.head as Entity
    assertSame(entity, entity.features.head.type)
  }
</code></pre>
  </li>
  <li>
    <p>After saving the Xtend file, it is time to run the test. Select <em>Run As → JUnit Test</em> from the editor’s context menu. </p>
  </li>
</ol>

<h2 id="tutorial-validation">Creating Custom Validation Rules</h2>

<p>One of the main advantages of DSLs is the possibility to statically validate domain specific constraints. This can be achieved by means of static analysis. Because this is a common use case, Xtext provides a dedicated hook for this kind of validation rules. In this lesson, we want to ensure that the name of an <em>Entity</em> starts with an upper-case letter and that all features have distinct names across the inheritance relationship of an <em>Entity</em>.</p>

<p>Try to locate the class <em>DomainmodelValidator</em> in the package <em>org.eclipse.xtext.example.validation</em>. It can be found in the language plug-in. Defining the constraint itself is only a matter of a few lines of code:</p>

<pre><code class="language-xtend">@Check
def void checkNameStartsWithCapital(Entity entity) {
  if (!Character::isUpperCase(entity.getName().charAt(0))) {
    warning("Name should start with a capital", 
      DomainmodelPackage$Literals::TYPE__NAME);
  }
}
</code></pre>

<p>Any name for the method will do. The important thing is the <a href="https://github.com/eclipse/xtext/blob/v2.9.0/plugins/org.eclipse.xtext/src/org/eclipse/xtext/validation/Check.java">Check</a> annotation that advises the framework to use the method as a validation rule. If the name starts with a lower case letter, a warning will be attached to the name of the <em>Entity</em>.</p>

<p>The second validation rule is straight-forward, too. We traverse the inheritance hierarchy of the <em>Entity</em> and look for features with equal names.</p>

<pre><code class="language-xtend">@Check
def void checkFeatureNameIsUnique(Feature f) {
	var superEntity = (f.eContainer() as Entity).getSuperType();
	while (superEntity != null) {
		for (other : superEntity.getFeatures()) {
			if (f.getName().equals(other.getName())) {
				error("Feature names have to be unique", DomainmodelPackage$Literals::FEATURE__NAME);
				return;
			}
		}
		superEntity = superEntity.getSuperType();
	}
}
</code></pre>

<p>The sibling features, that are defined in the same entity, are automatically validated by the Xtext framework. Therefore, they do not have to be checked twice.</p>

<hr />

<p><strong><a href="104_jvmdomainmodel.html">Next Chapter: Five simple steps to your JVM language</a></strong></p>


</body>
</html>
