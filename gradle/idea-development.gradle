buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath "org.xtext:xtext-idea-gradle-plugin:${versions.ideaGradlePluginVersion}"
	}
}

apply plugin: org.xtext.gradle.idea.IdeaDevelopmentPlugin

ideaDevelopment {
	ideaVersion = versions.ideaVersion
	pluginDependencies {
		id 'junit'
	}
}

dependencies {
	ideaProvided ideaDevelopment.toolsJar()
}

javadoc.classpath += configurations.ideaProvided

def isTestProject = name.endsWith('tests')
def isIntellijPlugin = file('META-INF/plugin.xml').exists() && !isTestProject

if (isIntellijPlugin) {
	apply plugin: org.xtext.gradle.idea.IdeaPluginPlugin
}

configurations.all {
	// IntelliJ's test infrastructure is not compatible with ASM 5
	exclude group: 'org.ow2.asm'
}

tasks.withType(Test) {
	minHeapSize = "512m"
	maxHeapSize = "1g"
	jvmArgs '-ea'
	/*
	 * We have an active annotation that allows us to delegate
	 * to existing tests and run them in an IntelliJ context.
	 * The Delegates themselves are not valid JUnit tests, but
	 * look like one. So we need to filter them out here
	 */
	exclude {it.name.endsWith('Delegate.class')}
}
