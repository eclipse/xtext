/*
 * Root project configuration that is reused by subprojects to apply the Xtend compiler.
 */
if (!hasProperty('JENKINS_URL')) {
	ext.JENKINS_URL = 'http://services.typefox.io/open-source/jenkins'
}

// The repositories to query when constructing the Xtend compiler classpath
repositories {
	jcenter()
	maven {
		name 'xtend-bootstrap'
		url "$JENKINS_URL/job/xtend-bootstrap/lastStableBuild/artifact/build-result/maven-repository/"
	}
}

// The Xtend compiler version to use
def bootstrapXtendVersion = versions.xtext_bootstrap

configurations {
	xtendCompiler {
		description 'Bootstrap dependencies for the Xtend compiler'
		resolutionStrategy {
			eachDependency {
				if (requested.group == 'org.eclipse.xtext' || requested.group == 'org.eclipse.xtend')
					useVersion(bootstrapXtendVersion)
				if (requested.group == 'com.google.inject' && requested.name == 'guice')
					useVersion(versions.guice)
				if (requested.group == 'org.eclipse.jdt' && requested.name == 'org.eclipse.jdt.core')
					useVersion(versions.jdtCore)
			}
		}
		exclude group: 'asm'
	}
}

dependencies {
	xtendCompiler "org.eclipse.xtend:org.eclipse.xtend.core:$bootstrapXtendVersion"
	xtendCompiler "org.eclipse.xtext:org.eclipse.xtext.smap:$bootstrapXtendVersion"
	xtendCompiler "org.xtext:xtext-gradle-builder:$versions.xtext_gradle_plugin"
}
