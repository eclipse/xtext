/*
 * Configuration for projects with default layout (src/main/java etc.)
 */

ext.eclipseProjectLayout = false

sourceSets {
	main {
		java.srcDir 'src/main/xtext-gen'
		xtendOutputDir = 'src/main/xtend-gen'
	}
	test {
		java.srcDir 'src/test/xtext-gen'
		xtendOutputDir = 'src/test/xtend-gen'
	}
}

if (name.contains('example')) {
	tasks.withType(Javadoc) {
		enabled = false
	}
	javadocJar.enabled = false
	uploadArchives.enabled = false
} else {
	artifacts.archives javadocJar
}

// Configuration of meta data required by the Eclipse IDE
eclipse {
	classpath {
		plusConfigurations += [configurations.mwe2Runtime]
		file.whenMerged {
			entries.each { source ->
				if (source.kind == 'src' && source.path.endsWith('-gen') && !source.path.equals('xtend-gen') ) {
					source.entryAttributes['ignore_optional_problems'] = 'true'
				}
				if (source.kind == 'output') {
					source.path = 'bin/main'
				}
			}
		}
	}
	project {
		natures 'org.eclipse.xtext.ui.shared.xtextNature'
		buildCommands.add(0,new org.gradle.plugins.ide.eclipse.model.BuildCommand('org.eclipse.xtext.ui.shared.xtextBuilder'))
	}
}