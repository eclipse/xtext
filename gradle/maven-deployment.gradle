//------------------------------------------------------
// Create additional sources and Javadoc artifacts

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
	from ('.') {
		include 'schema/**', 'model/**'
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

//------------------------------------------------------
// Sign the artifacts if the signing info is complete

apply plugin: 'signing'
signing {
	required { gradle.taskGraph.hasTask('uploadArchives') }
	sign configurations.archives
}
if (!project.hasProperty('signing.secretKeyRingFile') || !project.property('signing.secretKeyRingFile')
		|| !project.hasProperty('signing.keyId') || !project.property('signing.keyId')
		|| !project.hasProperty('signing.password') || !project.property('signing.password')) {
	signArchives.enabled = false
}

//------------------------------------------------------
// Configuration function for generated POMs

def configurePom = { pom ->
	pom.project {
		if (project.hasProperty('title')) {
			name = project.title
			description = project.description
		}
		packaging 'jar'
		url 'https://www.eclipse.org/Xtext/'
		licenses {
			license {
				name 'Eclipse Public License, Version 1.0'
				url 'http://www.eclipse.org/legal/epl-v10.html'
			}
		}
		scm {
			connection "scm:git:git@github.com:eclipse/${rootProject.name}.git"
			developerConnection "scm:git:git@github.com:eclipse/${rootProject.name}.git"
			url "git@github.com:eclipse/${rootProject.name}.git"
		}
	}
	apply from: "${rootDir}/gradle/developers.gradle", to: pom
}

// We need to wait until the project's own build file has been executed
// so we can use the title and description settings for setting up Maven publishing.
afterEvaluate {
	
	//------------------------------------------------------
	// Task for copying to a local Maven repository
	
	task createLocalMavenRepo(type: Upload) {
		group = 'Upload'
		description = 'Create or update the local Maven repository'
		configuration = configurations.archives
		repositories.mavenDeployer {
			repository(url: "file:" + file("../build/maven-repository"))
			configurePom(pom)
		}
	}
	
	//------------------------------------------------------
	// Task for uploading to a remote Maven repository
	
	uploadArchives.repositories.mavenDeployer {
		beforeDeployment { deployment -> signing.signPom(deployment) }
		repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
			if (project.hasProperty('sonatypeUserName') && project.hasProperty('sonatypePassword')) {
				authentication(userName: sonatypeUserName, password: sonatypePassword)
			}
		}
		snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
			if (project.hasProperty('sonatypeUserName') && project.hasProperty('sonatypePassword')) {
				authentication(userName: sonatypeUserName, password: sonatypePassword)
			}
		}
		configurePom(pom)
	}

}