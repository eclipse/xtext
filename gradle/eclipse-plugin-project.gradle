/**
 * This allows us to reference projects built by tycho/eclipse from
 * normal Java projects, for instance the intellij plugins.
 * We read the manifest and build.properties to build a Jar file
 * just like tycho would do it and then use that in the downstream projects.
 */

import org.apache.ivy.osgi.core.ManifestParser

buildscript {
	repositories.jcenter()
	dependencies {
		classpath (group: 'org.apache.ivy', name: 'ivy', version: '2.4.0')
	}
}

apply plugin: 'java'
apply from: "${rootDir}/gradle/dependency-mapping.gradle"

dependencies {
	def manifestFile = file('META-INF/MANIFEST.MF')
	def manifest = ManifestParser.parseManifest(manifestFile)
	manifest.requires
		.collect{toDependency(it)}
		.findAll {it != null}
		.each {
			add('compile', it)
		}
}

task configureJarFromBuildProperties {
	doLast {
		jar {
			//TODO get rid of this distinction by reconfiguring
			//the Eclipse output folder for the mavenized projects
			if(isBuildServer) {
				from('target/classes')
			} else {
				from('bin')
			}

			manifest {
				from('META-INF/MANIFEST.MF')
			}
			Properties buildProperties = new Properties()
			File propertiesFile = file('build.properties')
			propertiesFile.withInputStream {
					buildProperties.load(it)
			}
			def binIncludes = buildProperties.get("bin.includes").split(',').findAll{it != "."}
			binIncludes.each{ include ->
				if (project.file(include).isDirectory()) {
					from(include) {
						into(include)
					}
				} else {
					from(include)
				}
			}
			duplicatesStrategy 'exclude'
		}
	}
}

jar.dependsOn(configureJarFromBuildProperties)
if (isBuildServer) {
	jar.dependsOn(':maven:compileEclipsePlugins')
}

//skip compilation, done by Eclipse/Maven
compileJava.enabled = false
processResources.enabled = false

//skip Eclipse tasks for now
eclipseProject.enabled = false
cleanEclipseProject.enabled = false
eclipseJdt.enabled = false
cleanEclipseJdt.enabled = false
eclipseClasspath.enabled = false
cleanEclipseClasspath.enabled = false
