plugins.withType(JavaBasePlugin) {
	sourceCompatibility = '1.8'
}

tasks.withType(JavaCompile) {
	options.encoding = 'ISO-8859-1'
}

tasks.withType(Javadoc) {
	options.encoding = 'ISO-8859-1'
	options.tags = [ 'noreference', 'noextend', 'noimplement', 'noinstantiate', 'nooverride', 'model', 'generated', 'ordered' ] 
	options.addStringOption('Xdoclint:none', '-quiet')
}

sourceSets {
    mwe2 {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    optional {
    	description 'Dependencies required at build time, but not exported into meta data'
        extendsFrom compile
    }
	mwe2Compile.extendsFrom mainCompile
	mwe2Runtime.extendsFrom mainRuntime
    
	/*
	 * Put any unwanted transitive dependencies here, they will be excluded from all projects
	 */
    all {
		exclude group: 'org.antlr', module: 'stringtemplate'
		exclude module: 'cglib'
	}
}

sourceSets.main.compileClasspath += configurations.optional
sourceSets.test.compileClasspath += configurations.optional
sourceSets.test.runtimeClasspath += configurations.optional
javadoc.classpath += configurations.optional

eclipse {
	classpath.plusConfigurations += [configurations.optional]
	classpath.plusConfigurations += [configurations.mwe2Runtime]
	classpath.plusConfigurations += [configurations.mwe2Compile]

	project {
		natures 'org.eclipse.xtext.ui.shared.xtextNature'
		buildCommand 'org.eclipse.xtext.ui.shared.xtextBuilder'
	}
}
