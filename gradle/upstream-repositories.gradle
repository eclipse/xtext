/*
 * The build uses either public snapshots or branch-specific repositories on the Jenkins
 * build server as upstream repositories. Use the argument '-PuseJenkinsSnapshots=true'
 * to enable the Jenkins repositories. In this case you can select an upstream branch
 * with the argument '-PupstreamBranch=<branch name>'. Without that argument, the
 * upstream branch is selected automatically based on the version string.
 */

if (!hasProperty('upstreamBranch')) {
	def versionSplit = version.split('\\.')
	if (versionSplit.length == 4)
		ext.upstreamBranch = 'milestone_' + version
	else if (versionSplit[2] == '0-SNAPSHOT')
		ext.upstreamBranch = 'master'
	else if (versionSplit[2].endsWith('-SNAPSHOT'))
		ext.upstreamBranch = 'maintenance_' + version.substring(0, version.lastIndexOf('.'))
	else
		ext.upstreamBranch = 'release_' + version
}

def jenkinsPipelineRepo = { jobName -> "http://services.typefox.io/open-source/jenkins/job/$jobName/job/$upstreamBranch/lastStableBuild/artifact/build/maven-repository/" }

repositories {
	jcenter()
	// Temporarily include Maven Central since jcenter is lagging behind
	maven {
		name 'maven-central'
		url 'http://repo1.maven.org/maven2/'
	}
	// Artifacts of xtext-idea are not available as public snapshots, so Jenkins snapshots are the default
	if (!hasProperty('useJenkinsSnapshots') || findProperty('useJenkinsSnapshots') == 'true') {
		maven { url jenkinsPipelineRepo('lsp4j') }
		maven { url jenkinsPipelineRepo('xtext-lib') }
		maven { url jenkinsPipelineRepo('xtext-core') }
		maven { url jenkinsPipelineRepo('xtext-extras') }
		maven { url jenkinsPipelineRepo('xtext-eclipse') }
		maven { url jenkinsPipelineRepo('xtext-idea') }
	} else {
		mavenLocal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	}
}
