/*******************************************************************************
 * Copyright (c) 2015, 2019 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
module org.eclipse.xtext.web.example.entities.GenerateEntities

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var baseName = "org.eclipse.xtext.web.example.entities"

Workflow {

	component = XtextGenerator {
		configuration = EntitiesGeneratorModule {
			project = StandardProjectConfig {
				rootPath = rootPath
				baseName = baseName
				genericIde = {
					enabled = true
				}
				web = {
					enabled = true
					root = "../org.eclipse.xtext.web.example.jetty"
					src = "../org.eclipse.xtext.web.example.jetty/src/main/java"
					srcGen = "../org.eclipse.xtext.web.example.jetty/src/main/xtext-gen"
					assets = "../org.eclipse.xtext.web.example.jetty/src/main/webapp"
				}
				runtimeTest = {
					enabled = true
					root = "../org.eclipse.xtext.xbase.web"
					src = "../org.eclipse.xtext.xbase.web/src/test/java"
					srcGen = "../org.eclipse.xtext.xbase.web/src/test/xtext-gen"
				}
			}
			code = {
				preferXtendStubs = false
				encoding = "ISO-8859-1"
				fileHeader = 
"/*******************************************************************************
 * Copyright (c) 2015, 2019 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/"
			}
		}
		cleaner = {
			exclude = "AbstractStatemachineWebModule.java"
		}
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/web/example/entities/Entities.xtext"
			fileExtensions = "entities"
			referencedResource = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.genmodel"

			// Java API to access grammar elements (required by several other fragments)
			fragment = grammarAccess.GrammarAccessFragment2 {}

			// generates Java API for the generated EPackages
			fragment = ecore.EMFGeneratorFragment2 {}

			fragment = serializer.SerializerFragment2 {
				generateStub = true
			}

			// a custom ResourceFactory for use with EMF
			fragment = resourceFactory.ResourceFactoryFragment2 {}

			// The antlr parser generator fragment.
			fragment = parser.antlr.XtextAntlrGeneratorFragment2 {
			}

			// Xtend-based API for validation
			fragment = validation.ValidatorFragment2 {
				//composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}

			// scoping and exporting API
			fragment = scoping.ImportNamespacesScopingFragment2 {}
			fragment = exporting.QualifiedNamesFragment2 {}

			// generator API
			fragment = generator.GeneratorFragment2 {}

			// formatter API
			fragment = formatting.Formatter2Fragment2 {
				generateXtendStub = false
			}

			// testing API
			fragment = junit.JUnitFragment {
				generateStub = false
			}

			// Xbase support
			fragment = types.TypesGeneratorFragment2 {}
			fragment = xbase.XbaseGeneratorFragment2 {
				generateXtendInferrer = false
			}
			fragment = xbase.XtypeGeneratorFragment2 {}

			// JavaScript-based syntax highlighting
			fragment = web.WebIntegrationFragment {
				framework = "Orion"
				highlightingPath = "xtext/entities-syntax.js"
			}
			fragment = web.WebIntegrationFragment {
				framework = "Ace"
				highlightingPath = "xtext/ace-mode-entities.js"
			}
			fragment = web.WebIntegrationFragment {
				framework = "CodeMirror"
				highlightingPath = "xtext/cm-mode-entities.js"
			}
		}
	}
}
